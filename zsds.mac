	ASEG
	EXTRN	@PLOT,@LINE,@PRNTC,@PRINT,@BGSTR,@BGSIZ,@SCRMS
	EXTRN	@MASC8,@STCOL,@XY,@XX,@YY,@BGSYM
	EXTRN	IPLAY9,PLAY9,IPLAY5,PLAY5
	ORG	100H
	JMP	START
	JMP	LABIRP
GAME:	DW	2700H
PRINTC:	JMP	@PRNTC
PRINT:	JMP	@PRINT
PLOT:	JMP	@PLOT
LINE:	JMP	@LINE
COLOR:	JMP	@STCOL
SCR2:	JMP	@SCRMS
SCR3:	JMP	@MASC8
	JMP	@BGSYM
	JMP	@BGSTR
	DW	@BGSIZ
KEYS:	DI
	LDA	15
	STA	3BH
	OUT	10H
	EI
OKEYS:	CALL	0
	DI
	PUSH	PSW
	LDA	3CH
	STA	3BH
	OUT	10H
	POP	PSW
	EI
	RET
INKEY:	DI
	LDA	15
	STA	3BH
	OUT	10H
	EI
OINKEY:	CALL	0
	DI
	PUSH	PSW
	LDA	3CH
	STA	3BH
	OUT	10H
	POP	PSW
	EI
	RET
FILE	EQU	2180H
POGON	EQU	FILE+576
DES	EQU	POGON+288
ORELP	EQU	DES+352
CDISK:	DB	0
COLOR1:	DB	0A4H,192,-1,208,98H,34,-1,-1
	DB	0,192,7,12,197,34,54,-1
COLOR2:	DB	0,208,7,12,197,34,3FH,-1
	DB	0,0,0,0,0,0,0,0
PALET0:	DB	0,0,-1,-1,-1,-1,-1,-1,0,0,-1,-1,-1,-1,-1,-1
STR00:	DB	27,'Y40'
	DB	'Информация',0
STR0:	DB	'ДЕСАНТНИК',0
STR1:	DB	27,'Y',2EH,35H,'Меню',0
STRJ0:	DB	1BH,59H,2CH,39H
	DB	'Варианты',0
STR3:	DB	1BH,59H,2EH,36H
	DB	' УСПИД-Take '
	DB	1BH,59H,2FH,36H
	DB	' Джойстик П '
	DB	1BH,59H,30H,36H
	DB	' Джойстик С '
STRH0:	DB	27,'Y',26H,31H,'Помощь',0
STRH1:	DB	27,'Y',28H,2CH,0
STRH2:	DB	27,'Y',2FH,2CH,0
STRH3:	DB	27,'Y',28H,2CH,0
STRA:	DB	1BH,59H,36H,31H
	DB 'Десантник V1.0'
	DB 1BH,59H,37H,28H,'(c) Вьюнов В.А.,21 апреля 1996 г.',0
STRGDB:	DB 'До свидания !',0
STRRQ:	DB 1BH,59H,2CH,28H,'Но сначала введите пароль.'
	DB 1BH,59H,2DH,28H,'>',0
SELJS	EQU	10H
LEVEL	EQU	11H
FLINV	EQU	12H
FLSTRT:	DB	0
STRBS:	DB '   Ошибка BIOS !',0
STRER0:	DB 1BH,59H,29H,2DH,7,0
STRNF:	DB 'Немогу найти файл !',0
START0:	LXI	SP,0
	CALL	LDFILS
START:	DI
	LXI	SP,8000H
	XRA	A
	STA	3CH
	STA	3BH
	OUT	10H
	MVI	A,0B6H
	OUT	8
	MVI	A,76H
	OUT	8
	MVI	A,36H
	OUT	8
	CALL	STRDS
	LXI	H,DYSPLY
	SHLD	39H
	MVI	A,0C3H
	STA	38H
	EI
	LXI	H,START
	SHLD	1
	CALL	CLS
	LXI	D,COLOR1
	XRA	A
	CALL	SCR0
	MVI	A,15
	CALL	COLOR
	LXI	H,0
	SHLD	@XY
	LXI	B,-1
	MVI	A,1
	CALL	LINE
	LXI	H,0F04H
	SHLD	@BGSIZ
	MVI	A,8
	CALL	COLOR
	LXI	H,4F12H
	SHLD	@XY
	LXI	H,STR0
	PUSH	H
	CALL	@BGSTR
	MVI	A,3
	CALL	COLOR
	LXI	H,5011H
	SHLD	@XY
	POP	H
	CALL	@BGSTR
	LXI	H,STR00
	SHLD	WTITLE
	LXI	H,404H
	LXI	B,0FB39H
	MVI	E,0
	MVI	A,1
	CALL	WINDOW
	MVI	A,1FH
	CALL	COLOR
	LXI	H,STRA
	CALL	PRINT
	CALL	OREL
	CALL	WAITK
	CALL	IPLAY9
ST00:	DI
	CALL	PLAY9
	EI
	HLT
	CALL	INKEY
	INR	A
	JZ	ST00
BEGIN:	LXI	H,STR1
	SHLD	WTITLE
	LXI	H,3E30H
	LXI	B,0E075H
	MVI	E,0
	MVI	A,14
	CALL	WINDOW
BEG02:	CALL	PICT
BEG04:	LXI	H,4848H
	SHLD	MOVYY
	LXI	H,0D838H
	SHLD	MOVXX
	LHLD	MENCUR
	SHLD	CURXY
BEG10:	CALL	MOVCUR
	LHLD	CURXY
	SHLD	MENCUR
	MOV	B,L
	MVI	C,7
	LXI	H,SELMEN
	CALL	SELECT
	JC	BEG10
	PCHL
MOVCUR:	CALL	STCUR
	CALL	KEY
	PUSH	PSW
	CALL	CLCUR
	POP	PSW
	CPI	1BH
	STC
	RZ
	CPI	13
	RZ
	CPI	20H
	RZ
MOVC02:	LXI	H,MOVC10
	PUSH	H
	STA	MOVKEY
MOVC04:	CPI	18H
	JZ	MOVR
	CPI	8
	JZ	MOVL
	CPI	19H
	JZ	MOVU
	CPI	1AH
	JZ	MOVD
	POP	H
	JMP	MOVCUR
MOVC10:	CALL	STCUR
	LXI	H,280H
	DCX	H
	MOV	A,H
	ORA	L
	JNZ	$-3
	DI
	LDA	15
	OUT	10H
	LHLD	DSFKEY
	MOV	C,M
	XRA	A
	OUT	10H
	EI
	INR	C
	JZ	MOVC11
	CALL	INKEY
	MOV	C,A
	LDA	MOVKEY
	CMP	C
	JZ	MOVC12
	INR	C
	JZ	MOVC12
MOVC11:	CALL	CLCUR
	JMP	MOVCUR
MOVC12:	CALL	CLCUR
	LDA	MOVKEY
	JMP	MOVC02
MOVKEY:	DB	0
DSFKEY:	DW	0
MOVR:	LXI	H,CURXX
	LDA	LASXX
	CMP	M
	JZ	MOVR10
	RC
	INR	M
	RET
MOVL:	LXI	H,CURXX
	LDA	FIRXX
	CMP	M
	JZ	MOVL10
	RNC
	DCR	M
	RET
MOVU:	LXI	H,CURYY
	LDA	LASYY
	CMP	M
	JZ	MOVU10
	RC
	INR	M
	RET
MOVU10:	LDA	FIRYY
	MOV	M,A
	RET
MOVD10:	LDA	LASYY
	MOV	M,A
	RET
MOVL10:	LDA	LASXX
	MOV	M,A
	RET
MOVR10:	LDA	FIRXX
	MOV	M,A
	RET
MOVD:	LXI	H,CURYY
	LDA	FIRYY
	CMP	M
	JZ	MOVD10
	RNC
	DCR	M
	RET
STCUR:	LDA	CURXX
	MOV	C,A
	ANI	7
	MOV	E,A
	MVI	D,0
	LXI	H,TABM
	DAD	D
	DAD	D
	MOV	A,M
	INX	H
	MOV	H,M
	MOV	L,A
	SHLD	MASC
	LXI	H,CURROR
	MOV	B,E
	MVI	A,7
	SUB	B
	ADD	L
	MOV	L,A
	MVI	A,0
	ADC	H
	MOV	H,A
	SHLD	CURRRC+1
	LDA	CURYY
	MOV	L,A
	MOV	A,C
	RRC
	RRC
	RRC
	ANI	1FH
	ADI	80H
	MOV	H,A
	SHLD	VIDCUR
	LXI	D,CURPOD
	CALL	CURSAV
	LXI	D,CURPIC
	CALL	CUROUT
	RET
CURRRC:	JMP	0
CURROR:	RRC
	RRC
	RRC
	RRC
	RRC
	RRC
	RRC
	RET
CURSAV:	PUSH	H
	MVI	B,4
CURS10:	PUSH	H
	MVI	C,7
CURS12:	MOV	A,M
	STAX	D
	INX	D
	INR	H
	MOV	A,M
	STAX	D
	INX	D
	DCR	H
	INR	L
	DCR	C
	JNZ	CURS12
	POP	H
	MVI	A,20H
	ADD	H
	MOV	H,A
	DCR	B
	JNZ	CURS10
	POP	H
	RET
CLCUR:	LHLD	VIDCUR
	LXI	D,CURPOD
CURPOP:	MVI	B,4
CURP10:	PUSH	H
	MVI	C,7
CURP12:	LDAX	D
	MOV	M,A
	INX	D
	INR	H
	LDAX	D
	MOV	M,A
	INX	D
	DCR	H
	INR	L
	DCR	C
	JNZ	CURP12
	POP	H
	MVI	A,20H
	ADD	H
	MOV	H,A
	DCR	B
	JNZ	CURP10
	RET
CUROUT:	PUSH	H
	LHLD	MASC
	MOV	B,H
	MOV	C,L
	POP	H
	CALL	CUROR
	MVI	A,3
CURO10:	PUSH	PSW
	MVI	A,20H
	ADD	H
	MOV	H,A
	CALL	CURXOR
	POP	PSW
	DCR	A
	JNZ	CURO10
	RET
CUROR:	MVI	A,7
	PUSH	H
	PUSH	D
CURO20:	PUSH	PSW
	LDAX	D
	CALL	CURRRC
	PUSH	PSW
	ANA	B
	ORA	M
	MOV	M,A
	INR	H
	POP	PSW
	ANA	C
	ORA	M
	MOV	M,A
	DCR	H
	INX	D
	INR	L
	POP	PSW
	DCR	A
	JNZ	CURO20
	POP	D
	POP	H
	RET
CURXOR:	MVI	A,7
	PUSH	H
	PUSH	D
CURX10:	PUSH	PSW
	LDAX	D
	CALL	CURRRC
	PUSH	PSW
	ANA	B
	CMA
	ANA	M
	MOV	M,A
	INR	H
	POP	PSW
	ANA	C
	CMA
	ANA	M
	MOV	M,A
	DCR	H
	INR	L
	POP	PSW
	INX	D
	DCR	A
	JNZ	CURX10
	POP	D
	POP	H
	RET
SELECT:	MOV	A,B
	CMP	M
	INX	H
	JC	SEL10
	CMP	M
	JC	SEL20
SEL10:	INX	H
	INX	H
	INX	H
	DCR	C
	JNZ	SELECT
	STC
	RET
SEL20:	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	ANA	A
	RET
MENCUR:	DB	56,48H
CURXY:
CURXX:	DB	0
CURYY:	DB	0
MOVYY:
FIRYY:	DB	0
LASYY:	DB	0
MOVXX:
FIRXX:	DB	0
LASXX:	DB	0
SELMEN:	DB	56,72
	DW	PEREL
	DB	80,96
	DW	LABIR
	DB	104,120
	DW	JOYST
	DB	128,144
	DW	HELP
	DB	152,168
	DW	EXIT
	DB	176,192
	DW	INVER
	DB	200,216
	DW	SETLEV
LABIR:	XRA	A
	STA	PAROL
	MVI	A,1
	JMP	STAGE
LABIRP:	MVI	A,1
	STA	PAROL
	JMP	STAGE
PEREL:	XRA	A
STAGE:	STA	STAD
	LXI	SP,8000H
	CALL	CLS
	LXI	D,COLOR2
	XRA	A
	CALL	SCR0
	MVI	A,7
	CALL	COLOR
	LXI	H,0
	SHLD	@XY
	LXI	B,-1
	MVI	A,1
	CALL	LINE
	MVI	A,6
	CALL	COLOR
	LXI	H,0E850H
	SHLD	@XY
	LXI	H,202H
	SHLD	@BGSIZ
	LXI	H,STRST1
	LDA	STAD
	ANA	A
	JZ	$+6
	LXI	H,STRST2
	CALL	@BGSTR
	LXI	H,0D850H
	SHLD	@XY
	LXI	H,STRST0
	CALL	@BGSTR
	LXI	H,LMASC2
	CALL	SCR3
	LXI	H,0A028H
	SHLD	@XY
	MVI	A,7
	CALL	COLOR
	LXI	H,504H
	SHLD	@BGSIZ
	LXI	H,STRPER
	LDA	STAD
	ANA	A
	JZ	STAG10
	LXI	H,0A018H
	SHLD	@XY
	LXI	H,STRLAB
STAG10:	CALL	@BGSTR
	LXI	H,LMASC0
	CALL	SCR3
	MVI	A,3
	CALL	COLOR
	LXI	H,202H
	SHLD	@XY
	LXI	B,0FD98H
	PUSH	H
	PUSH	B
	MVI	A,2
	CALL	LINE
	POP	B
	POP	H
	SHLD	@XY
	MVI	A,36H
	CALL	COLOR
	MVI	A,1
	CALL	LINE
	LXI	H,0E292H
	LXI	D,DES
	LXI	B,327H
	CALL	SPRIT
	LXI	H,POGON
	LXI	D,96
	LDA	LEVEL
	ANA	A
	JZ	STAG11
	DAD	D
	DCR	A
	JZ	STAG11
	DAD	D
STAG11:	LXI	D,0E692H
	XCHG
	LXI	B,210H
	CALL	SPRIT
	LXI	H,6B0FH
	SHLD	@XY
	LXI	B,2993H
	MVI	A,1
	CALL	LINE
	LXI	H,822FH
	SHLD	@XY
	LXI	B,4193H
	MVI	A,1
	CALL	LINE
	LXI	H,STRZV0
	CALL	PRINT
	LXI	H,STRZV1
	LDA	LEVEL
	ANA	A
	JZ	STAG12
	LXI	H,STRZV2
	DCR	A
	JZ	STAG12
	LXI	H,STRZV3
STAG12:	CALL	PRINT
	LXI	H,STRTXT
	CALL	PRINT
	LDA	STAD
	LXI	H,TXTPER
	ANA	A
	JZ	$+6
	LXI	H,TXTLAB
STAG14:	MVI	B,40
STAG15:	MOV	A,M
	ANA	A
	JZ	STAG20
	INX	H
	MOV	C,A
	CALL	PRINTC
	DCR	B
	JNZ	STAG15
	MVI	C,10
	CALL	PRINTC
	MVI	C,18H
	CALL	PRINTC
	JMP	STAG14
STAG20:	CALL	IPLAY5
STAG21:	DI
	CALL	PLAY5
	EI
	JNC	STAG20
	HLT
	CALL	INKEY
	INR	A
	JZ	STAG21
	LHLD	GAME
	PUSH	H
	XCHG
	LXI	H,FILPER
	LDA	STAD
	ANA	A
	JZ	$+6
	LXI	H,FILLAB
	CALL	LOAD
	LDA	STAD
	ANA	A
	RZ
	LDA	PAROL
	ANA	A
	RNZ
	MVI	C,1FH
	CALL	PRINTC
	MVI	A,7
	CALL	COLOR
	LXI	H,STRRQ
	CALL	PRINT
	LXI	H,80H
	PUSH	H
	CALL	INPUT
	POP	D
	LXI	H,SHPARL
	MVI	B,10
STAG30:	LDAX	D
	XRI	0A5H
	CMP	M
	INX	H
	INX	D
	JNZ	START
	DCR	B
	JNZ	STAG30
	RET
INPUT:	CALL	KEY
	MOV	M,A
	INX	H
	CPI	13
	RZ
	MOV	C,A
	CALL	PRINTC
	JMP	INPUT
SHPARL:	DB 0E7H,0C9H,0D0H,0C0H,85H,47H,60H,77H,60H,71H
PAROL:	DB	0
FILPER:	DB	0,'STAGE1  DES'
FILLAB:	DB	0,'STAGE2  DES'
LMASC0:	DW	-1,-1,-1,-1
LMASC2:	DW	0FFH,0FFH,0FFH,0FFH
SPRIT:	PUSH	B
	PUSH	H
	MVI	A,3
SPRT02:	PUSH	PSW
	PUSH	H
	MOV	C,B
SPRT10:	LDAX	D
	MOV	M,A
	INX	D
	INR	H
	DCR	C
	JNZ	SPRT10
	POP	H
	MVI	A,-20H
	ADD	H
	MOV	H,A
	POP	PSW
	DCR	A
	JNZ	SPRT02
	POP	H
	POP	B
	DCR	L
	DCR	C
	JNZ	SPRIT
	RET
HELP:	LXI	H,8855H
	LXI	D,0A000H
	LXI	B,1471H
	PUSH	H
	PUSH	D
	PUSH	B
	CALL	SCRSAV
	LXI	H,STRH0
	SHLD	WTITLE
	LXI	H,5544H
	LXI	B,0DAC5H
	MVI	E,1
	MVI	A,4
	CALL	WINDOW
	MVI	A,2
	CALL	@SCRMS
	CALL	COLOR
	LXI	H,TXTHLP
	SHLD	PNTXT
	CALL	HELP00
	LXI	H,0BABAH
	SHLD	MOVYY
	LXI	H,0D545H
	SHLD	MOVXX
	LHLD	HLPCUR
	SHLD	CURXY
HELP10:	CALL	MOVCUR
	LHLD	CURXY
	SHLD	HLPCUR
	JC	HELP20
	MOV	B,L
	MVI	C,3
	LXI	H,SELHLP
	CALL	SELECT
	JC	HELP10
	PCHL
HELP20:	POP	B
	POP	D
	POP	H
	CALL	SCRLOA
	MVI	A,15
	CALL	@SCRMS
	JMP	BEG04
HLPCUR:	DB	0D2H,0BAH
HLPD:	LHLD	PNTXT
	LXI	D,24*8
	DAD	D
	MOV	A,M
	ANA	A
	JZ	HELP10
	PUSH	H
	LHLD	PNTXT
	LXI	D,24
	DAD	D
	SHLD	PNTXT
	LXI	H,0C9A5H
	LXI	D,0C9AFH
	MVI	B,80
HLPD10:	CALL	LINCOP
	DCR	L
	DCR	E
	DCR	B
	JNZ	HLPD10
	LXI	H,STRH2
HLPD11:	CALL	PRINT
	POP	H
	MVI	D,24
HLPD12:	MOV	C,M
	INX	H
	CALL	PRINTC
	DCR	D
	JNZ	HLPD12
	JMP	HELP10
HLPU:	LHLD	PNTXT
	DCX	H
	MOV	A,M
	ANA	A
	JZ	HELP10
	LXI	D,-23
	DAD	D
	SHLD	PNTXT
	PUSH	H
	LXI	D,0C960H
	LXI	H,0C96AH
	MVI	B,70
HLPU10:	CALL	LINCOP
	INR	L
	INR	E
	DCR	B
	JNZ	HLPU10
	LXI	H,STRH3
	JMP	HLPD11
LINCOP:	PUSH	H
	PUSH	D
	MVI	C,18
LINC10:	MOV	A,M
	STAX	D
	INR	H
	INR	D
	DCR	C
	JNZ	LINC10
	POP	D
	POP	H
	RET
SELHLP:	DB	45H,4CH
	DW	HELP20
	DB	0CAH,0D0H
	DW	HLPD
	DB	0D0H,0D6H
	DW	HLPU
HELP00:	MVI	D,8
	LXI	B,STRH1+2
	MVI	A,28H
	STAX	B
HELP02:	PUSH	H
	LXI	H,STRH1
	CALL	PRINT
	POP	H
	MVI	E,24
HELP04:	MOV	C,M
	INX	H
	CALL	PRINTC
	DCR	E
	JNZ	HELP04
	PUSH	H
	LXI	H,STRH1+2
	INR	M
	POP	H
	DCR	D
	JNZ	HELP02
	RET
SETLEV:	LXI	H,LEVEL
	MOV	A,M
	INR	A
	CPI	3
	JC	$+4
	XRA	A
	MOV	M,A
	JMP	BEG02
PICT:	LXI	H,0E764H
	LXI	D,FILE
	MVI	B,6
PICT10:	CALL	PICT20
	INR	H
	INR	H
	INR	H
	DCR	B
	JNZ	PICT10
	PUSH	H
	LDA	LEVEL
	INR	A
	LXI	H,POGON-96
	LXI	D,96
PICT12:	DAD	D
	DCR	A
	JNZ	PICT12
	XCHG
	POP	H
	CALL	PICT20
	RET
PICT20:	PUSH	H
	PUSH	B
	CALL	PICT30
	MVI	C,16
PICT21:	PUSH	H
	MVI	B,3
PICT22:	LDAX	D
	INX	D
	MOV	M,A
	INR	H
	LDAX	D
	MOV	M,A
	INX	D
	MVI	A,-21H
	ADD	H
	MOV	H,A
	DCR	B
	JNZ	PICT22
	POP	H
	DCR	L
	DCR	C
	JNZ	PICT21
	POP	B
	POP	H
	RET
PICT30:	PUSH	H
	MOV	A,H
	ANI	9FH
	MOV	H,A
	LXI	B,10FFH
PICT32:	MOV	M,C
	INR	H
	MOV	M,C
	DCR	H
	DCR	L
	DCR	B
	JNZ	PICT32
	POP	H
	RET
WINDOW:	SHLD	@XY
	CALL	@STCOL
	MVI	A,2
	CALL	LINE
	SHLD	@XY
	MVI	A,15
	CALL	@STCOL
	MVI	A,1
	CALL	LINE
	MOV	A,C
	SUI	12
	MOV	H,A
	SHLD	@XY
	MVI	A,2
	CALL	LINE
	INR	H
	INR	L
	SHLD	@XY
	DCR	B
	DCR	C
	MVI	A,0F8H
	CALL	@STCOL
	PUSH	H
	LHLD	WTITLE
	CALL	PRINT
	POP	H
	MVI	A,1
	CALL	LINE
	MOV	A,L
	ADI	8
	MOV	L,A
	SHLD	@XY
	MVI	A,1
	CALL	LINE
	PUSH	B
	PUSH	H
	MOV	A,L
	SUI	6
	MOV	L,A
	INR	H
	INR	H
	INR	H
	INR	H
	SHLD	@XY
	POP	B
	PUSH	B
	MVI	A,6
	ADD	B
	DCR	C
	DCR	C
	MOV	B,C
	MOV	C,A
	MVI	A,2
	CALL	LINE
	POP	H
	POP	B
	INR	E
	DCR	E
	RZ
	MOV	A,B
	SUI	8
	MOV	B,A
	SHLD	@XY
	MVI	A,1
	CALL	LINE
	PUSH	H
	PUSH	B
	CALL	WIND20
	MVI	D,3
WIND12:	SHLD	@XY
	XRA	A
	CALL	LINE
	INR	L
	INR	H
	INR	C
	DCR	B
	DCR	D
	JNZ	WIND12
	POP	B
	POP	H
	MOV	A,B
	SUI	8
	MOV	B,A
	SHLD	@XY
	MVI	A,1
	CALL	LINE
	CALL	WIND20
	MVI	D,3
	MOV	A,H
	ADI	2
	MOV	H,A
	MOV	A,C
	ADI	2
	MOV	C,A
WIND14:	SHLD	@XY
	XRA	A
	CALL	LINE
	INR	L
	DCR	H
	DCR	B
	DCR	C
	DCR	D
	JNZ	WIND14
	RET
WIND20:	MOV	H,C
	MOV	L,B
	INR	L
	MOV	A,H
	SUI	6
	MOV	H,A
	MOV	C,A
	MOV	A,B
	ADI	6
	MOV	B,A
	RET
WAITK:	CALL	KEYS
	ANA	A
	RZ
	CALL	INKEY
	JMP	WAITK
KEY:	CALL	KEYS
	ANA	A
	JZ	KEY
	JMP	INKEY
EXIT:	LXI	D,PALET0
	MVI	A,10H
	CALL	SCR0
	CALL	CLS
	LXI	H,502CH
	SHLD	@XY
	MVI	A,6
	CALL	COLOR
	LXI	H,502H
	SHLD	@BGSIZ
	LXI	H,STRGDB
	CALL	@BGSTR
	DI
	MVI	A,80H
	STA	3EH
	MVI	A,23H
	STA	3CH
	MVI	C,0
	CALL	5
	LDA	CDISK
	STA	4
	JMP	0
INVER:	LDA	FLINV
	XRI	2FH
	STA	FLINV
	LXI	H,COLOR1
	MVI	B,32
	PUSH	H
INV10:	MOV	A,M
	CMA
	MOV	M,A
	INX	H
	DCR	B
	JNZ	INV10
	POP	D
	XRA	A
	CALL	SCR0
	JMP	BEG04
STRDS:	LDA	FLSTRT
	ANA	A
	RNZ
	STA	SELJS
	STA	LEVEL
	STA	FLINV
	CMA
	STA	FLSTRT
	MVI	E,12
	MVI	C,2
	CALL	5
	DI
	LHLD	9
	LXI	D,37H
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	SHLD	OINT7+1
	XRA	A
	STA	3CH
	LDA	4
	STA	CDISK
	LDA	4CH
	MVI	A,81H
	STA	3EH
	MVI	C,0
	CALL	5
	LDA	15
	OUT	10H
	LHLD	0A002H
	LXI	D,12H
	DAD	D
	SHLD	OKEYS+1
	LXI	D,9
	DAD	D
	SHLD	OINKEY+1
	INX	H
	INX	H
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	SHLD	DSPDAT
	LXI	D,26
	DAD	D
	SHLD	DSFKEY
	LXI	H,V5
	SHLD	6
	SHLD	9
	XRA	A
	OUT	10H
	RET
SCR0:	DI
	PUSH	PSW
	LDA	15
	OUT	10H
	LHLD	DSPDAT
	INX	H
	INX	H
	INX	H
	MVI	M,80H
	INX	H
	MVI	B,16
	XCHG
	CALL	COPY10
	LXI	H,4
	DAD	D
	POP	PSW
	MOV	M,A
	XRA	A
	OUT	10H
	EI
	RET
COPY:	MVI	B,12
COPY10:	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	COPY10
	RET
CLEAR:	LXI	H,68H
	LXI	B,18H
CL10:	MOV	M,B
	INX	H
	DCR	C
	JNZ	CL10
	RET
OREL:	LXI	D,ORELP
	LXI	H,2F1AH
	SHLD	OREL12
	LXI	H,0A10AH
	CALL	OREL10
	PUSH	H
	LXI	H,0FF3EH
	SHLD	OREL12
	POP	H
	LXI	B,-2000H
	DAD	B
	CALL	OREL10
	LXI	B,4000H
	DAD	B
	CALL	OREL10
	PUSH	H
	LXI	H,3EH
	SHLD	OREL12
	POP	H
	LXI	B,2000H
	DAD	B
OREL10:	MVI	B,4
	PUSH	H
OREL11:	MVI	C,32
	PUSH	H
OREL12:	LDAX	D
	CMA
	MOV	M,A
	INX	D
	INR	L
	DCR	C
	JNZ	OREL12
	POP	H
	INR	H
	DCR	B
	JNZ	OREL11
	POP	H
	RET
DYSPLY:	PUSH	PSW
	LDA	15
	OUT	10H
OINT7:	CALL	0
	LDA	3BH
	OUT	10H
	POP	PSW
	EI
	RET
V5:	DI
	LDA	15
	STA	3BH
	OUT	10H
	EI
	CALL	0A006H
	DI
	PUSH	PSW
	LDA	3CH
	STA	3BH
	OUT	10H
	EI
	LDA	3DH
	ANA	A
	JNZ	BSERR
	POP	PSW
	RET
BSERR:	LXI	H,STRBS
	XRA	A
	STA	3DH
BSERR0:	LXI	SP,8000H
	PUSH	H
	MVI	A,7
	CALL	COLOR
	LXI	H,STRER0
	CALL	PRINT
	POP	H
	CALL	PRINT
	CALL	KEY
	JMP	START
NOFILE:	LXI	H,STRNF
	JMP	BSERR0
SCRSAV:	DI
SCRS05:	MOV	A,C
	PUSH	H
SCRS10:	PUSH	PSW
	PUSH	H
	PUSH	B
	PUSH	D
	MOV	C,M
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	B,M
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	E,M
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	D,M
	MVI	A,23H
	OUT	10H
	POP	H
	MOV	M,C
	INX	H
	MOV	M,B
	INX	H
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	XRA	A
	OUT	10H
	XCHG
	POP	B
	POP	H
	POP	PSW
	INR	L
	DCR	A
	JNZ	SCRS10
	POP	H
	INR	H
	DCR	B
	JNZ	SCRS05
	EI
	RET
SCRLOA:	DI
	XCHG
SCRL05:	MOV	A,C
	PUSH	D
SCRL10:	PUSH	PSW
	PUSH	B
	PUSH	D
	MVI	A,23H
	OUT	10H
	MOV	C,M
	INX	H
	MOV	B,M
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	XTHL
	PUSH	H
	XRA	A
	OUT	10H
	MOV	M,C
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	M,B
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	M,E
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	M,D
	POP	D
	INR	E
	POP	H
	POP	B
	POP	PSW
	DCR	A
	JNZ	SCRL10
	POP	D
	INR	D
	DCR	B
	JNZ	SCRL05
	EI
	RET
JOYST:	LXI	H,9050H
	LXI	D,0A000H
	LXI	B,0A3FH
	PUSH	H
	PUSH	D
	PUSH	B
	CALL	SCRSAV
	LXI	H,STRJ0
	SHLD	WTITLE
	LXI	H,5080H
	LXI	B,0CF89H
	MVI	A,10
	MVI	E,0
	CALL	WINDOW
JOYS10:	LXI	H,0C6C6H
	SHLD	MOVXX
	LXI	H,8252H
	SHLD	MOVYY
	LHLD	JOYCUR
	SHLD	CURXY
JOYS12:	CALL	JOYPAN
JOYS13:	CALL	MOVCUR
	LHLD	CURXY
	SHLD	JOYCUR
	JC	JOYS20
	MOV	B,H
	LXI	H,SELJOY
	MVI	C,4
	CALL	SELECT
	JC	JOYS13
	PCHL
JOYCUR:	DB	0C6H,5CH
JOYPAN:	LDA	SELJS
	INR	A
	MOV	D,A
	MVI	E,3
	LXI	H,STR3
JOYP10:	DCR	D
	JZ	JOYP20
	MVI	A,0AEH
	CALL	COLOR
	CALL	JOYP12
JOYP14:	DCR	E
	JNZ	JOYP10
	RET
JOYP12:	MVI	B,16
JOYP13:	MOV	C,M
	CALL	PRINTC
	INX	H
	DCR	B
	JNZ	JOYP13
	RET
JOYP20:	MVI	A,0DEH
	CALL	COLOR
	MVI	B,2
	CALL	JOYP13
	MOV	A,M
	PUSH	PSW
	MVI	B,1
	CALL	JOYP13
	MOV	A,M
	PUSH	PSW
	MVI	B,13
	CALL	JOYP13
	MVI	A,15
	CALL	COLOR
	POP	PSW
	SUI	20H
	ADD	A
	MOV	B,A
	ADD	A
	ADD	B
	MOV	B,A
	POP	PSW
	PUSH	H
	SUI	20H
	ADD	A
	MOV	H,A
	ADD	A
	ADD	A
	ADD	H
	MOV	H,A
	MVI	A,-1
	SUB	H
	MOV	H,A
	MOV	L,B
	MOV	A,L
	ADI	71
	MOV	B,A
	MOV	C,H
	SHLD	@XY
	XRA	A
	CALL	LINE
	MOV	A,C
	SUI	9
	MOV	C,A
	XRA	A
	CALL	LINE
	MVI	A,8
	CALL	COLOR
	MOV	B,L
	XRA	A
	CALL	LINE
	MOV	C,H
	DCR	H
	CALL	LINE
	POP	H
	JMP	JOYP14
JOYS:	MVI	A,2
	STA	SELJS
	JMP	JOYS12
JOYP:	MVI	A,1
	STA	SELJS
	JMP	JOYS12
JOYU:	XRA	A
	STA	SELJS
	JMP	JOYS12
JOYS20:	POP	B
	POP	D
	POP	H
	CALL	SCRLOA
	JMP	BEG04
SELJOY:	DB	51H,5BH
	DW	JOYS
	DB	5BH,65H
	DW	JOYP
	DB	65H,6FH
	DW	JOYU
	DB	78H,82H
	DW	JOYS20
CLS:	MVI	A,15
	CALL	SCR2
	MVI	C,1FH
	CALL	PRINTC
	DI
	LDA	15
	OUT	10H
	LHLD	DSPDAT
	LXI	D,25
	DAD	D
	MVI	M,-1
	XRA	A
	OUT	10H
	EI
	RET
LDFILS:	LXI	D,FILE
	LXI	H,GRAF
	CALL	LOAD
	RET
LOAD:	PUSH	D
	LXI	D,5CH
	PUSH	D
	CALL	COPY
	CALL	CLEAR
	POP	D
	MVI	C,15
	CALL	5
	POP	D
	INR	A
	JZ	NOFILE
LOAD10:	PUSH	D
	MVI	C,1AH
	CALL	5
	LXI	D,5CH
	MVI	C,20
	CALL	5
	POP	D
	LXI	H,128
	DAD	D
	XCHG
	ANA	A
	JZ	LOAD10
	RET
GRAF:	DB	0,'GR      ZSD'
DSPDAT:	DW	0
WTITLE:	DW	0
PNTXT:	DW	0
TABM:	DW	0FF00H,7F80H,3FC0H,1FE0H
	DW	0FF0H,7F8H,3FCH,1FEH
CURPIC:	DB 2,24H,38H,3CH,78H,60H,80H
CURPOD:	DS	64
MASC:	DW	0
VIDCUR:	DW	0

	DB	0
TXTPER:	DB	'Ваша задача: на вертол╠те пересечь линию'
	DB	'фронта;  осуществить высадку в заданной '
	DB	'точке,около входа в подземный бункер шта'
	DB	'ба противника.',0
TXTLAB:	DB	'Итак,вы находитесь у входа в подземелье,'
	DB	'ведущее к главному штабу противника. Под'
	DB	'земелье охраняется минными заграждениями'
	DB	'и вооруж╠нными солдатами.У вас есть с со'
	DB	'бой бронежилет и автомат,они помогут вам'
	DB	'выдержать несколько попаданий пуль,и уни'
	DB	'чтожить часть охраны. Необходимо, пройдя'
	DB	'все уровни,добраться до главного штаба.',0
STRST0:	DB	'стадия',0
STRST1:	DB	'Первая',0
STRST2:	DB	'Вторая',0
STRPER:	DB	'ПЕРЕЛ╟Т',0
STRLAB:	DB	'ЛАБИРИНТ',0
STRZV0:	DB	27,'Y',2CH,31H,'Господин',0
STRZV1:	DB	27,'Y',2DH,32H,'майор !',0
STRZV2:	DB	27,'Y',2DH,2FH,'подполковник !',0
STRZV3:	DB	27,'Y',2DH,30H,'полковник !',0
STRTXT:	DB	27,'Y',2FH,21H,0
	DB	0
TXTHLP:	DB	' Игра "ДЕСАНТНИК" содер-'
	DB	'жит две стадии.Чтобы сра'
	DB	'зу начать со второй ста-'
	DB	'дии необходимо знать па-'
	DB	'роль.Он сообщается толь-'
	DB	'ко после успешного прео-'
	DB	'доления первой стадии иг'
	DB	'ры.Программа имеет наст-'
	DB	'ройку на тип джойстика и'
	DB	'тип монитора (с инверси-'
	DB	'ей RGB).                '
	DB	'   Клавиши управления:  '
	DB	'В "ПЕРЕЛ╟ТЕ": клавиши со'
	DB	'стрелками - перемещение,'
	DB	'<ТАБ> - горизонтальный,а'
	DB	'<ПРОБЕЛ> - диагональный '
	DB	'выстрелы.               '
	DB	'В "ЛАБИРИНТЕ":клавиши со'
	DB	'стрелками - движение,   '
	DB	'<ТАБ> - выстрел влево,а '
	DB	'<ПРОБЕЛ> - вправо.      '
	DB	'Игра основана на оригина'
	DB	'льной серии игр SW corp.'
	DB	'для компьютеров БК.     '
	DB	'     Желаю успехов !    '
	DB	0
STAD:	DB	0
