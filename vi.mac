	GLOBAL	VIEW
	EXTERNAL COPY,KEY,HDCMP,HLA,FRBIOS,CLEAR
RNREC	EQU	5CH+33
BUFSTR	EQU	0E600H
VIEW:	CALL	CLEAR
	LXI	D,5CH
	MVI	C,15
	CALL	5
	INR	A
	RZ
	LXI	D,MDMA
	MVI	C,1AH
	CALL	5
	CALL	GTDSPB
	LXI	H,-1
	SHLD	RNREC
	LXI	H,0
	SHLD	NXREC
	XRA	A
	STA	STRPNT
	STA	NSMECH
	STA	RDOWN
	STA	GLSM
	STA	BUFSTR+1
	DCR	A
	STA	STRCNT
	CALL	PAGD
VIEW0:	LDA	STRCNT
	ADI	20H
	STA	STRV+2
	LXI	H,STRV
	CALL	PRINT
VIEW1:	CALL	KEY
	MOV	B,A
	IN	1
	ANI	20H
	MOV	A,B
	JZ	KCNTR
	CPI	13
	JZ	MASK
	CPI	12
	JZ	NXSEAR
	CPI	19H
	JZ	PUP
	CPI	1AH
	JZ	PDOWN
	ANA	A
	JZ	KOD
	CPI	8
	JZ	LEFT
	CPI	18H
	JZ	RIGHT
	CPI	1BH
	RZ
	JMP	VIEW1
MASK:	LXI	H,STR1
	CALL	PRINT
	LXI	D,BUFSTR
	MVI	C,10
	MVI	A,128
	STAX	D
	CALL	5
	JMP	RGHT10
FSEAR:	LXI	H,0
	SHLD	NXREC
	SHLD	NREC
	DCX	H
	SHLD	RNREC
	XRA	A
	STA	SMECH
	JMP	NXSR00
NXSEAR:	MVI	A,1
	CALL	GTELEM
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	A,M
	STA	SMECH
	XCHG
	SHLD	NXREC
	SHLD	NREC
	LXI	H,-1
	SHLD	RNREC
NXSR00:	LXI	H,BUFSTR+1
	MOV	C,M
	MVI	B,0
	INX	H
	DAD	B
	MVI	M,0
	LDA	SMECH
	STA	TSMECH
	LHLD	NREC
	SHLD	TNREC
NXSR10:	LHLD	TNREC
	SHLD	NREC
	CALL	READZ
	LXI	D,BUFSTR+2
	LDA	TSMECH
	MOV	B,A
	DCR	B
NXSR11:	INR	B
	JP	NXSR12
	CALL	NXSR30
NXSR12:	MOV	A,B
	CALL	HLAM
	CPI	1AH
	JZ	NXSR90
	CPI	-1
	JZ	NXSR90
	LDAX	D
	ANA	A
	JZ	NXSR40
	CMP	M
	JNZ	NXSR20
	INX	D
	JMP	NXSR11
NXSR40:	LHLD	TNREC
	SHLD	NXREC
	LDA	TSMECH
	STA	SMECH
	SHLD	NREC
	CALL	READZ
	LXI	H,NXSR42
	PUSH	H
	PUSH	B
	LDA	SMECH
	MOV	B,A
	LXI	H,MDMA
	CALL	HLA
	JMP	PRSU11
NXSR42:	LDA	SMECH
	STA	NSMECH
	XRA	A
	STA	RDOWN
	JMP	PDWN10
NXSR20:	LXI	H,TSMECH
	INR	M
	JP	NXSR10
	MVI	M,0
	LHLD	TNREC
	INX	H
	SHLD	TNREC
	JMP	NXSR10
NXSR30:	PUSH	D
	CALL	NREADZ
	POP	D
	MVI	B,0
	RZ
	POP	PSW
	JMP	NXSR90
HLAM:	LXI	H,MDMA
	CALL	HLA
	MOV	A,M
	RET
NXSR90:	MVI	E,7
	MVI	C,2
	CALL	5
	JMP	RGHT10
KOD:	LDA	KODT
	XRI	1
	STA	KODT
	JMP	RGHT10
RIGHT:	LXI	H,GLSM
	MOV	A,M
	ADI	32
	MOV	M,A
RGHT10:	XRA	A
	CALL	GTELEM
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	A,M
	STA	NSMECH
	XCHG
	SHLD	NXREC
	XRA	A
	STA	STRPNT
	STA	RDOWN
	DCR	A
	STA	STRCNT
	CALL	PAGD
	JMP	VIEW0
LEFT:	LXI	H,GLSM
	MOV	A,M
	SUI	32
	MOV	M,A
	JMP	RGHT10
KCNTR:	CPI	19H
	JZ	LUP
	CPI	12
	JZ	FSEAR
	CPI	1AH
	JNZ	VIEW0
LDOWN:	LDA	RDOWN
	ANA	A
	JNZ	VIEW0
	MVI	C,10
	CALL	PRNTC
	CALL	PRSTRD
	JMP	VIEW0
LUP:	XRA	A
	CALL	GTELEM
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	C,M
	MOV	A,C
	ORA	E
	ORA	D
	JZ	VIEW0
	XCHG
	SHLD	NXREC
	MOV	A,C
	STA	SMECH
	MVI	A,24
	CALL	GTELEM
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	A,M
	PUSH	D
	PUSH	PSW
	LXI	H,STRPNT
	DCR	M
	JP	$+5
	MVI	M,24
	CALL	PRSTRU
	LHLD	NXREC
	SHLD	NREC
	XCHG
	XRA	A
	CALL	GTELEM
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	LDA	SMECH
	MOV	M,A
	CALL	SHFSCR
	CALL	PRSTR
	CALL	SHFS10
	POP	PSW
	STA	NSMECH
	POP	H
	SHLD	NXREC
	LXI	H,STRCNT
	MOV	A,M
	CPI	24
	JZ	$+4
	INR	A
	MOV	M,A
	CPI	24
	JNZ	VIEW0
	XRA	A
	STA	RDOWN
	JMP	VIEW0
GTDSPB:	DI
	LDA	15
	OUT	10H
	LHLD	0A002H
	LXI	D,1EH
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	SHLD	DSPDAT
	LDA	3CH
	OUT	10H
	EI
	RET
GTCURX:	DI
	PUSH	H
	LDA	15
	OUT	10H
	LHLD	DSPDAT
	INX	H
	MOV	H,M
	LDA	3CH
	OUT	10H
	MOV	A,H
	POP	H
	EI
	RET
SHFSCR:	DI
	LDA	15
	OUT	10H
	LHLD	DSPDAT
	INX	H
	MOV	A,M
	STA	DSPX
	INX	H
	MOV	A,M
	STA	DSPY
	PUSH	H
	LDA	3CH
	OUT	10H
	MVI	C,13
	CALL	PRNTC
	MVI	C,16H
	CALL	PRNTC
	DI
	LDA	15
	OUT	10H
	POP	H
	MOV	D,H
	MOV	E,L
	LXI	B,23
	DAD	B
	MOV	A,M
	ADI	10
	MOV	M,A
	STAX	D
	OUT	3
	XCHG
	DCX	H
	MVI	M,0
	LDA	3CH
	OUT	10H
	RET
SHFS10:	DI
	LDA	15
	OUT	10H
	LHLD	DSPDAT
	INX	H
	LDA	DSPX
	MOV	M,A
	INX	H
	LDA	DSPY
	ADI	10
	MOV	M,A
	LDA	3CH
	OUT	10H
	EI
	RET
GTELEM:	LXI	H,STRCNT
	CMP	M
	JZ	GTEL10
	JC	GTEL10
	MOV	A,M
	DCR	A
GTEL10:	LXI	H,STRPNT
	ADD	M
	CPI	25
	JC	$+5
	SUI	25
	MOV	H,A
	ADD	A
	ADD	H
	LXI	H,MASSTR
	CALL	HLA
	RET
PDOWN:	LDA	RDOWN
	ANA	A
	JNZ	VIEW0
	MVI	A,24
	CALL	GTELEM
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	A,M
	STA	NSMECH
	XCHG
	SHLD	NXREC
PDWN10:	XRA	A
	STA	STRPNT
	DCR	A
	STA	STRCNT
	CALL	PAGD
	JMP	VIEW0
PUP:	XRA	A
	CALL	GTELEM
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	C,M
	MOV	A,C
	ORA	E
	ORA	D
	JZ	VIEW0
	MOV	A,C
	STA	SMECH
	XRA	A
	STA	RDOWN
	XCHG
	SHLD	NXREC
	CALL	PAGU
	LDA	SMECH
	STA	NSMECH
	LHLD	NREC
	SHLD	NXREC
	JMP	PDWN10
PAGU:	MVI	B,24
PAGU10:	CALL	PRSTRU
	RC
	DCR	B
	JNZ	PAGU10
	RET
PRSTRU:	PUSH	B
	LHLD	NXREC
	SHLD	NREC
	CALL	READZ
PRSU10:	LXI	H,MDMA
	LDA	SMECH
	MOV	B,A
	CALL	HLA
	CALL	PRSU20
	JC	PRSU30
	CALL	PRSU20
	JC	PRSU30
	MOV	A,M
	CPI	13
	JNZ	PRSU12
PRSU11:	CALL	PRSU20
	JC	PRSU30
PRSU12:	MOV	A,M
	CPI	10
	JZ	PRSU14
	CPI	13
	JNZ	PRSU11
PRSU14:	CALL	PRST30
	MOV	A,B
	STA	SMECH
	POP	B
	XRA	A
	RET
PRSU30:	CALL	PRST30
	MOV	A,B
	STA	SMECH
	POP	B
	STC
	RET
PRSU20:	DCX	H
	ANA	A
	DCR	B
	RP
	CALL	PREADZ
	RC
	LXI	H,MDMA+127
	MVI	A,127
	STA	SMECH
	MOV	B,A
	XRA	A
	RET
PREADZ:	LHLD	NREC
	MOV	A,L
	ORA	H
	STC
	RZ
	DCX	H
	SHLD	NREC
	SHLD	NXREC
	SHLD	RNREC
	LXI	D,5CH
	MVI	C,33
	CALL	5
	ANA	A
	STC
	RNZ
	CMC
	RET
PAGD:	MVI	C,12
	CALL	PRNTC
	MVI	B,25
	LDA	RDOWN
	ANA	A
	RNZ
PAGD10:	CALL	PRSTRD
	JC	PAGD12
	DCR	B
	RZ
	MVI	C,10
	CALL	PRNTC
	JMP	PAGD10
PAGD12:	MVI	A,-1
	STA	RDOWN
	RET
PRST02:	LXI	H,STRPNT
	PUSH	PSW
	INR	M
	MOV	A,M
	CPI	25
	JC	$+5
	MVI	M,0
	POP	PSW
	RET
GETN:	LHLD	NXREC
	XCHG
	LDA	STRCNT
	INR	A
	CPI	25
	JC	GETN02
	CALL	PRST02
	DCR	A
GETN02:	STA	STRCNT
	CALL	GTELEM
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	LDA	NSMECH
	MOV	M,A
	LHLD	NXREC
	SHLD	NREC
	LDA	NSMECH
	STA	SMECH
	RET
PRSTRD:	CALL	GETN
PRSTR:	PUSH	B
	CALL	READZ
	LXI	H,MDMA
	LDA	SMECH
	MOV	B,A
	CALL	HLA
	LDA	GLSM
	ANA	A
	JZ	PRST19
	MOV	C,A
PRST16:	CALL	PRST1A
	JC	PRST22
	JNZ	PRST17
	CPI	13
	JZ	PRST20
	JMP	PRST19
PRST17:	LXI	H,MDMA
	LDA	SMECH
	MOV	B,A
	CALL	HLA
	JMP	PRST16
PRST19:	MVI	C,79
	CALL	PRST10
	JC	PRST22
	JZ	PRST18
	LXI	H,MDMA
	LDA	SMECH
	MOV	B,A
	CALL	HLA
	JMP	PRST19+2
PRST18:	CPI	13
	JZ	PRST20
	JMP	PRST24
PRST1A:	MOV	A,M
	ANA	A
	CPI	13
	RZ
	CPI	9
	JZ	PRST1D
	CPI	1AH
	STC
	RZ
	CPI	-1
	STC
	RZ
PRST1E:	CALL	PRST30
	RC
	DCR	C
	JNZ	PRST1A
	XRA	A
	RET
PRST1D:	PUSH	B
	MOV	A,C
	MOV	B,A
	ANI	7
	MOV	C,A
	MVI	A,8
	SUB	C
	MOV	C,A
	MOV	A,B
	SUB	C
	POP	B
	MOV	C,A
	JNC	PRST1E
	XRA	A
	RET
PRST10:	MOV	A,M
	ANA	A
	CPI	13
	RZ
	CPI	1AH
	STC
	RZ
	CPI	-1
	STC
	RZ
	PUSH	B
	MOV	C,A
	CPI	20H
	JNC	PRST1C
	CPI	9
	JZ	PRST1C
	PUSH	B
	MVI	C,1
	CALL	PRNTC
	POP	B
PRST1C:	CALL	PRNTC
	POP	B
	CALL	GTCURX
	ANA	A
	CMP	C
	RNC
	CALL	PRST30
	RC
	JP	PRST10
	ORI	1
	RET
PRST24:	CALL	PRST1A
	JC	PRST22
	JMP	PRST20
PRST30:	INX	H
	ANA	A
	INR	B
	RP
	PUSH	B
	CALL	NREADZ
	POP	B
	MVI	A,0
	STA	SMECH
	LXI	H,MDMA
	MOV	B,A
	STC
	RNZ
	ORI	80H
	RET
PRST20:	MVI	C,16H
	;ALL	PRNTC
	CALL	PRST30
	JC	PRST22
	MOV	A,M
	CPI	10
	JNZ	PRST21
	CALL	PRST30
	JC	PRST22
PRST21:	MOV	A,B
	STA	NSMECH
	POP	B
	XRA	A
	RET
PRST22:	MVI	C,16H
	;ALL	PRNTC
	POP	B
	STC
	RET
READZ:	LHLD	RNREC
	XCHG
	LHLD	NREC
	CALL	HDCMP
	RZ
READZ1:	SHLD	RNREC
	LXI	D,5CH
	MVI	C,33
	CALL	5
	ANA	A
	RET
NREADZ:	LHLD	NREC
	INX	H
	SHLD	NXREC
	SHLD	NREC
	JMP	READZ1
PRNTC:	PUSH	H
	PUSH	D
	PUSH	B
	PUSH	PSW
	LDA	KODT
	ANA	A
	CNZ	ALT
	LXI	H,FRBIOS
	MVI	M,2
	INX	H
	MOV	M,C
	DCX	H
	XCHG
	LXI	B,980H
	CALL	5
	POP	PSW
	POP	B
	POP	D
	POP	H
	RET
ALT:	MOV	A,C
	CPI	80H
	RC
	CPI	0B0H
	JC	ALT10
	CPI	0E0H
	JC	ALT11
	CPI	0F0H
	JC	ALT12
	SUI	40H
	MOV	C,A
	RET
ALT11:	SUI	30H
	MOV	C,A
	RET
ALT12:	SUI	30H
ALT10:	CPI	0A0H
	PUSH	PSW
	ANI	9FH
	LXI	H,ALTTAB
	SUI	80H
	CALL	HLA
	MOV	C,M
	POP	PSW
	MOV	A,C
	RC
	SUI	20H
	MOV	C,A
	RET
PRINT:	MOV	A,M
	ANA	A
	RZ
	PUSH	H
	MOV	E,A
	MVI	C,2
	CALL	5
	POP	H
	INX	H
	JMP	PRINT
ALTTAB:	DB 'АБВГДЕЖЗИЙКЛМНОПРСТУФХЦЧШЩъЫЬЭЮЯ'
SMECH:	DB	0
NSMECH:	DB	0
NREC:	DW	0
NXREC:	DW	0
RDOWN:	DB	0
STRCNT:	DB	0
STRPNT:	DB	0
DSPDAT:	DW	0
DSPX:	DB	0
DSPY:	DB	0
GLSM:	DB	0
KODT:	DB	0
TSMECH:	DB	0
TNREC:	DW	0
STRV:	DB	27,'Y  ',0
STR1:	DB	12,'Введите образец: ',0
MASSTR:	DS	75
MDMA	EQU	80H
