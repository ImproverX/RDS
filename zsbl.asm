	ORG	5400H
	LXI	SP,8000H
	XRA	A
	OUT	10H
	JMP	START
TLEVLS:	EQU	3400H
AGAME:	EQU	1D00H
FILE:	EQU	1600H
PRINTC:	EQU	129H
PRINT:	EQU	138H
INIT:	EQU	120H
INBYT:	EQU	126H
OUTBYT:	EQU	12CH
KEY:	EQU	132H
HEXOUT:	EQU	135H
INKEY:	EQU	13BH
PLOT:	EQU	13EH
LINE:	EQU	141H
COLOR:	EQU	156H
HDCMP:	EQU	186H
SCR0:	EQU	189H
SCR2:	EQU	18CH
BUFJ:	EQU	0A000H
BUF2:	EQU	0C700H
GAME:	JMP	AGAME
EDITOR:	EQU	3000H
COLOR1:	DB	12,192,7,12,197,34,54,255
	DB	192,0,34,255,7,255,54,255
PALET0:	DB	0,0,-1,-1,-1,-1,-1,-1,0,0,-1,-1,-1,-1,-1,-1
ADRNS:	DW	0
NADRNS:	DW	0
LMUS:	DB	0
STR1:	DB	1BH,59H,2BH,2BH
	DB	'МЕНЮ'
	DB	1BH,59H,2CH,29H
	DB	'  ИГРА  '
	DB	1BH,59H,2DH,29H
	DB	' ПОМОЩЬ '
	DB	1BH,59H,2EH,29H
	DB	'РЕДАКТОР'
	DB	1BH,59H,2FH,29H
	DB	'ДЖОЙСТИК'
	DB	1BH,59H,30H,29H
	DB	'ИНВЕРСИЯ'
	DB	1BH,59H,31H,29H
	DB	' ЗАПИСЬ '
	DB	1BH,59H,32H,29H
	DB	' ЧТЕНИЕ '
	DB	1BH,59H,33H,29H
	DB	'  В ОС  ',0
MENUS:	DB	1BH,59H,2CH,29H
	DB	'        ',0
STR3:	DB	1BH,59H,2AH,2FH
	DB	'ВАРИАНТЫ:'
	DB	1BH,59H,2BH,2EH
	DB	'УСПИД-Take'
	DB	1BH,59H,2CH,2EH
	DB	'Джойстик П'
	DB	1BH,59H,2DH,2EH
	DB	'Джойстик С'
	DB	0
STRCP:	DB	1BH,59H,2AH,21H
	DB	'Copyright (c) 1992,1994 '
	DB	'by Vewnov Vitaly.',0
STRA:	DB	1BH,59H,35H,32H,'ИНФОРМАЦИЯ',1BH,59H,36H,27H
	DB 'Boulder Dash V2.0,Дисковая,ОС РДС'
	DB 1BH,59H,37H,27H,'(c) Вьюнов В.А.,30 января 1995 г.',0
JOYSS:	DB	1BH,59H,2BH,2EH
	DB	'          ',0
STRGDB:	DB 'До свидания !',13,0
STR6:	DB	1BH,59H,29H,35H
	DB	'ВНИМАНИЕ !'
	DB	1BH,59H,2AH,2EH,0
STR7:	DB	1BH,59H,29H,37H
	DB	'ПОМОЩЬ',1BH,59H,2AH,2EH,0
STRSV:	DB 1BH,59H,29H,32H,'Запись уровней',0
STRLD:	DB 1BH,59H,29H,32H,'Чтение уровней',0
STRSL1:	DB 1BH,59H,2AH,2DH,'C:',1BH,59H,2BH,2DH,0
STRSL0:	DB 1BH,59H,32H,30H,'Имя файла:         '
	DB 8,8,8,8,8,8,8,8,0
PRS01:	DW	0
PRS02:	DW	0
JPSH01:	DW	0
JPSH02:	DW	0
JPSH03:	DW	0
SELJS:	EQU	10H
FSTART:	DB	0
STRFL:	DB 0,'????????BDL'
STRMD:	DB 'Нет места в каталоге.',0
STRMS:	DB ' Нет места на диске. ',0
STRBD:	DB 'Ошибка BDOS !',0
STRBS:	DB 'Ошибка BIOS !',0
STRER0:	DB 1BH,59H,2DH,33H,0
STRER1:	DB 1BH,59H,2EH,2FH,0
STRDE:	DB ' При обмене с диском.',0
BDINT:	DW	0
START:	LXI	SP,8000H
	MVI	A,0B6H
	OUT	8
	MVI	A,76H
	OUT	8
	MVI	A,36H
	OUT	8
	CALL	STRDS
	LXI	H,START
	SHLD	1
	CALL	INIT
	CALL	CLS
	LXI	D,COLOR1
	CALL	SCR0
	LXI	H,60H
ST10:	MOV	M,H
	INR	L
	JNZ	ST10
	LXI	H,FILE+620H
	CALL	EDITOR+11
	CALL	OREL
	MVI	A,6
	CALL	COLOR
	LXI	H,STRCP
	CALL	PRINT
	LXI	H,723H
	LXI	D,2CF8H
	LXI	B,50FH
	CALL	WINDOW
	MVI	A,57H
	CALL	COLOR
	LXI	H,STRA
	CALL	PRINT
	CALL	ONMUS
	CALL	WAITK
	CALL	OFMUS
BEGIN:	LXI	H,2E2EH
	LXI	D,09072H
	LXI	B,80FH
	CALL	WINDOW
	MVI	A,8FH
	CALL	COLOR
	LXI	H,STR1
	CALL	PRINT
BEG20:	MVI	A,4
	CALL	SCR2
	MVI	A,40H
	CALL	COLOR
	LXI	H,MENUS
	CALL	PRINT
	LDA	MENUS+2
	SUI	2CH
	MOV	D,A
BEG30:	CALL	INKEY
	CPI	-1
	JZ	BEG30
	PUSH	D
	PUSH	PSW
	MVI	A,4
	CALL	SCR2
	MVI	A,0
	CALL	COLOR
	LXI	H,MENUS
	CALL	PRINT
	MVI	A,15
	CALL	SCR2
	POP	PSW
	POP	D
	MVI	E,-1
	CPI	19H
	JZ	BEG40
	CPI	1AH
	MVI	E,1
	JZ	BEG40
	CPI	20H
	JNZ	BEG20
	MOV	A,D
	ANA	A
	JZ	GAME
	DCR	A
	JZ	HELP
	DCR	A
	JZ	EDIT
	DCR	A
	JZ	JOYST
	DCR	A
	JZ	INVERS
	DCR	A
	JZ	SAVE
	DCR	A
	JZ	LOAD
EXIT:	MVI	A,15
	CALL	SCR2
	LXI	D,PALET0
	CALL	SCR0
	CALL	CLS
	LXI	B,502CH
	MVI	A,2
	CALL	PLOT
	MVI	A,6
	CALL	COLOR
	LXI	B,502H
	MVI	A,3
	CALL	LINE
	LXI	H,STRGDB
	CALL	PRINT
	DI
	LDA	11
	CPI	'Р'
	JNZ	0
	MVI	A,80H
	STA	3EH
	MVI	A,23H
	STA	3CH
	MVI	C,0
	CALL	5
	MVI	C,13
	CALL	5
	LDA	STRSL1+4
	SUI	41H
	STA	4
	MOV	E,A
	MVI	C,14
	CALL	5
	JMP	0
ONMUS:	DI
	LHLD	39H
	SHLD	DYSP10+1
	LXI	H,DYSPLY
	SHLD	39H
	LXI	H,FILE+6A0H
	SHLD	ADRNS
	SHLD	NADRNS
	MVI	A,1
	STA	LMUS
	MVI	A,36H
	OUT	8
	EI
	RET
OFMUS:	DI
	LHLD	DYSP10+1
	SHLD	39H
	MVI	A,36H
	OUT	8
	EI
	RET
DYSPLY:	PUSH	H
	PUSH	D
	PUSH	B
	PUSH	PSW
	CALL	NOISE
	POP	PSW
	POP	B
	POP	D
	POP	H
DYSP10:	JMP	0
NOISE:	LXI	H,LMUS
	DCR	M
	RNZ
	MVI	M,10
	LHLD	ADRNS
	MOV	A,M
	INX	H
	ANA	A
	JNZ	NOIS10
	LHLD	NADRNS
	MOV	A,M
NOIS10:	SHLD	ADRNS
	MOV	L,A
	MVI	H,0
	DAD	H
	DAD	H
	MOV	D,H
	MOV	E,L
	DAD	H
	DAD	D
	MOV	A,L
	OUT	11
	MOV	A,H
	OUT	11
	RET
STRDS:	DI
	LDA	FSTART
	ANA	A
	RNZ
	CMA
	STA	FSTART
	LDA	11
	CPI	'Р'
	RNZ
	XRA	A
	STA	3CH
	MVI	A,81H
	STA	3EH
	MVI	C,0
	CALL	5
	MVI	C,25
	CALL	5
	MOV	E,A
	ADI	41H
	STA	STRSL1+4
	PUSH	D
	MVI	C,13
	CALL	5
	POP	D
	MVI	C,14
	CALL	5
	RET
COPY:	MVI	B,12
COPY10:	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	COPY10
	RET
CLEAR:	LXI	H,68H
	LXI	B,18H
CL10:	MOV	M,B
	INX	H
	DCR	C
	JNZ	CL10
	RET
OREL:	LXI	D,FILE+5A0H
	LXI	H,2F1AH
	SHLD	OREL12
	LXI	H,0A00AH
	PUSH	H
	CALL	OREL10
	LXI	H,0FF3EH
	SHLD	OREL12
	POP	H
	LXI	B,2000H
	DAD	B
OREL10:	MVI	B,4
OREL11:	MVI	C,32
	PUSH	H
OREL12:	LDAX	D
	CMA
	MOV	M,A
	INX	D
	INR	L
	DCR	C
	JNZ	OREL12
	POP	H
	INR	H
	DCR	B
	JNZ	OREL11
	RET
INVERS:	DI
	LDA	98EH
	ANA	A
	MVI	A,2FH
	JZ	$+4
	XRA	A
	STA	98EH
	EI
	JMP	BEG20
HELP:	LXI	H,934H
	LXI	D,BUFJ
	LXI	B,1671H
	CALL	JPUSH2
	LXI	H,STRHL
HELP10:	PUSH	H
	LXI	H,394EH
	LXI	D,0A4F3H
	LXI	B,010FH
	CALL	WINDOW
	MVI	A,1FH
	CALL	COLOR
	LXI	H,STR7
	CALL	PRINT
	POP	H
	CALL	EDIT05
	JNZ	HELP10
	CALL	JPOP
	JMP	BEG20
EDIT:	LXI	H,STRED
EDIT00:	PUSH	H
	LXI	H,394EH
	LXI	D,0A4F3H
	LXI	B,10FH
	CALL	WINDOW
	LXI	H,STR6
	MVI	A,1FH
	CALL	COLOR
	CALL	PRINT
	POP	H
	CALL	EDIT05
	JNZ	EDIT00
	JMP	EDITOR
EDIT05:	MVI	D,9
EDIT10:	MVI	E,25
EDIT20:	MOV	A,M
	ANA	A
	JZ	EDIT99
	MOV	C,A
	CALL	PRINTC
	INX	H
	DCR	E
	JNZ	EDIT20
	CALL	EDIT30
	DCR	D
	JNZ	EDIT10
EDIT22:	CALL	INKEY
	CPI	-1
	JZ	EDIT22
	CPI	1BH
	RET
EDIT30:	MVI	E,25
EDIT32:	MVI	C,8
	CALL	PRINTC
	DCR	E
	JNZ	EDIT32
	MVI	C,10
	JMP	PRINTC
EDIT99:	CALL	INKEY
	CPI	-1
	JZ	EDIT99
	XRA	A
	RET
SLWIN:	LXI	H,934H
	LXI	D,BUFJ
	LXI	B,1671H
	CALL	JPUSH2
SLWIN0:	LXI	H,3948H
	LXI	D,0A4F3H
	LXI	B,50FH
	CALL	WINDOW
	MVI	A,5FH
	JMP	COLOR
SAVE:	CALL	SLWIN
	LXI	H,SAVE05
	SHLD	BDINT
	LXI	H,STRSV
	CALL	PRINT
	LXI	H,STRSL1
	CALL	PRINT
	CALL	DIRL
SAVE05:	MVI	A,2FH
	CALL	COLOR
	LXI	H,STRSL0
	CALL	PRINT
	CALL	INPUT
	JC	SAVE99
	LDA	11
	CPI	'Р'
	JNZ	SAVE99
	LXI	H,SAVE20
	LDA	5EH
	CPI	':'
	JZ	SELD
	LXI	D,5CH
	MVI	C,17
	CALL	V5
	INR	A
	CNZ	ERAF
	LXI	D,5CH
	MVI	C,22
	CALL	V5
	INR	A
	LXI	H,STRMD
	JZ	BDERR
	LXI	D,TLEVLS
	MVI	B,64
SAVE10:	PUSH	D
	PUSH	B
	MVI	C,26
	CALL	V5
	LXI	D,5CH
	MVI	C,21
	CALL	V5
	LXI	H,STRMS
	ANA	A
	JNZ	BDERR
	LXI	H,128
	POP	B
	POP	D
	DAD	D
	XCHG
	DCR	B
	JNZ	SAVE10
	LXI	D,5CH
	MVI	C,16
	CALL	V5
SAVE20:	CALL	SLWIN0
	JMP	SAVE+3
SELD:	PUSH	H
	MVI	C,13
	CALL	5
	LDA	5DH
	MOV	D,A
	SUI	41H
	CPI	3
	JNC	SELD10
	MOV	E,A
	MOV	A,D
	STA	STRSL1+4
	MVI	C,14
	CALL	5
	RET
SELD10:	MVI	C,7
	CALL	PRINTC
	LDA	STRSL1+4
	SUI	41H
	MOV	E,A
	MVI	C,14
	JMP	5
SAVE99:	CALL	JPOP
	JMP	BEG20
ERAF:	LXI	D,5CH
	MVI	C,19
	CALL	V5
	RET
BDERR:	MVI	C,7
	CALL	PRINTC
	LXI	SP,8000H
	LXI	D,STRBD
	CALL	WINERR
	LHLD	BDINT
	PCHL
V5:	CALL	5
	PUSH	PSW
	LDA	3DH
	ANA	A
	JNZ	BSERR
	POP	PSW
	RET
BSERR:	MVI	C,7
	CALL	PRINTC
	LXI	SP,8000H
	LXI	D,STRBS
	LXI	H,STRDE
	CALL	WINERR
	LHLD	BDINT
	PCHL
WINERR:	MOV	B,H
	MOV	C,L
	LHLD	JPSH01
	PUSH	H
	LHLD	JPSH02
	PUSH	H
	LHLD	JPSH03
	PUSH	H
	PUSH	B
	PUSH	D
	LXI	H,0A5AH
	LXI	D,BUF2
	LXI	B,1323H
	CALL	JPUSH2
	LXI	H,6054H
	LXI	D,7CE0H
	LXI	B,60FH
	CALL	WINDOW
	MVI	A,6FH
	CALL	COLOR
	LXI	H,STRER0
	CALL	PRINT
	POP	H
	CALL	PRINT
	LXI	H,STRER1
	CALL	PRINT
	POP	H
	CALL	PRINT
	CALL	WAITK
	CALL	JPOP
	POP	H
	SHLD	JPSH03
	POP	H
	SHLD	JPSH02
	POP	H
	SHLD	JPSH01
	RET
INPUT:	LXI	H,80H
	MVI	B,0
INP10:	CALL	KEY
	CPI	1BH
	JZ	INP11
	CPI	13
	JZ	INP20
	CPI	8
	JZ	INP12
	CPI	7FH
	JZ	INP12
	CPI	20H
	JC	INP10
	CPI	5BH
	JNC	INP10
	MOV	C,A
	MOV	A,B
	CPI	8
	JNC	INP10
	INR	B
	MOV	M,C
	INX	H
	CALL	PRINTC
	JMP	INP10
INP11:	STC
	RET
INP12:	MOV	C,A
	MOV	A,B
	ANA	A
	JZ	INP10
	DCX	H
	DCR	B
	MVI	C,8
	CALL	PRINTC
	MVI	C,20H
	CALL	PRINTC
	MVI	C,8
	CALL	PRINTC
	JMP	INP10
INP20:	MVI	M,0
	LXI	D,5CH
	MVI	L,80H
	XCHG
	MVI	C,8
	MVI	M,0
	INX	H
INP22:	LDAX	D
	ANA	A
	JZ	INP23
	MOV	M,A
	INX	H
	INX	D
	DCR	C
	JNZ	INP22
INP24:	MVI	M,'B'
	INX	H
	MVI	M,'D'
	INX	H
	MVI	M,'L'
	CALL	CLEAR
	XRA	A
	RET
INP23:	MVI	M,20H
	INX	H
	DCR	C
	JNZ	INP23
	JMP	INP24
LOAD:	CALL	SLWIN
	LXI	H,LOAD05
	SHLD	BDINT
	LXI	H,STRLD
	CALL	PRINT
	LXI	H,STRSL1
	CALL	PRINT
	CALL	DIRL
LOAD05:	MVI	A,2FH
	CALL	COLOR
	LXI	H,STRSL0
	CALL	PRINT
	CALL	INPUT
	JC	LOAD99
	LDA	11
	CPI	'Р'
	JNZ	LOAD99
	LXI	H,LOAD20
	LDA	5EH
	CPI	':'
	JZ	SELD
	LXI	D,5CH
	MVI	C,15
	CALL	V5
	INR	A
	JZ	LOADNF
	LXI	D,TLEVLS
LOAD10:	PUSH	D
	MVI	C,26
	CALL	V5
	LXI	D,5CH
	MVI	C,20
	CALL	V5
	POP	D
	LXI	H,128
	DAD	D
	XCHG
	ANA	A
	JZ	LOAD10
LOAD99:	CALL	JPOP
	JMP	BEG20
LOAD20:	CALL	SLWIN0
	JMP	LOAD+3
LOADNF:	MVI	C,7
	CALL	PRINTC
	CALL	PRINTC
	JMP	LOAD05
DIRL:	LDA	11
	CPI	'Р'
	RNZ
	LXI	D,80H
	MVI	C,26
	CALL	5
	LXI	H,STRFL
	LXI	D,5CH
	PUSH	D
	CALL	COPY
	CALL	CLEAR
	POP	D
	MVI	C,17
	MVI	H,0
	PUSH	H
DIRL10:	CALL	5
	CPI	-1
	JZ	DIRL20
	ADD	A
	ADD	A
	ADD	A
	ADD	A
	ADD	A
	ADI	80H
	MOV	L,A
	MVI	H,0
	XTHL
	MOV	A,H
	CPI	3
	CZ	DIRL11
	INR	H
	MOV	E,H
	XTHL
	INX	H
	MVI	D,8
DIRL12:	MOV	C,M
	CALL	PRINTC
	INX	H
	DCR	D
	JNZ	DIRL12
	MVI	C,20H
	MOV	A,E
	CPI	3
	JZ	$+6
	CALL	PRINTC
	MVI	C,18
	JMP	DIRL10
DIRL11:	MVI	C,1AH
	CALL	PRINTC
	LXI	B,1A08H
DIRL13:	CALL	PRINTC
	DCR	B
	JNZ	DIRL13
	MVI	H,0
	RET
DIRL20:	POP	H
	RET
JPUSH:	LXI	H,0965H
	LXI	D,BUFJ
	LXI	B,0C3AH
	JMP	JPUSH2
JPUSH2:	SHLD	JPSH01
	XCHG
	SHLD	JPSH02
	XCHG
	PUSH	B
	XTHL
	SHLD	JPSH03
	XTHL
	POP	B
	PUSH	D
	LXI	D,8000H
	DAD	D
	POP	D
	DI
JPSH05:	MOV	A,C
	PUSH	H
JPSH10:	PUSH	PSW
	PUSH	H
	PUSH	B
	PUSH	D
	MOV	C,M
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	B,M
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	E,M
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	D,M
	MVI	A,23H
	OUT	10H
	POP	H
	MOV	M,C
	INX	H
	MOV	M,B
	INX	H
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	XRA	A
	OUT	10H
	XCHG
	POP	B
	POP	H
	POP	PSW
	INR	L
	DCR	A
	JNZ	JPSH10
	POP	H
	INR	H
	DCR	B
	JNZ	JPSH05
	EI
	RET
JPOP:	LHLD	JPSH03
	PUSH	H
	POP	B
	LXI	D,8000H
	LHLD	JPSH01
	DAD	D
	XCHG
	LHLD	JPSH02
	DI
JPOP05:	MOV	A,C
	PUSH	D
JPOP10:	PUSH	PSW
	PUSH	B
	PUSH	D
	MVI	A,23H
	OUT	10H
	MOV	C,M
	INX	H
	MOV	B,M
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	XTHL
	PUSH	H
	XRA	A
	OUT	10H
	MOV	M,C
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	M,B
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	M,E
	MVI	A,20H
	ADD	H
	MOV	H,A
	MOV	M,D
	POP	D
	INR	E
	POP	H
	POP	B
	POP	PSW
	DCR	A
	JNZ	JPOP10
	POP	D
	INR	D
	DCR	B
	JNZ	JPOP05
	EI
	RET
JOYST:	CALL	JPUSH
	LXI	H,6A4EH
	LXI	D,9A9AH
	LXI	B,0E0FH
	CALL	WINDOW
	MVI	A,0EFH
	CALL	COLOR
	LXI	H,STR3
	CALL	PRINT
JOYS10:	MVI	A,2
	CALL	SCR2
	MVI	A,0
	CALL	COLOR
	LXI	H,JOYSS
	CALL	PRINT
	LDA	JOYSS+2
	SUI	2BH
	MOV	D,A
JOYS20:	CALL	INKEY
	CPI	-1
	JZ	JOYS20
	PUSH	D
	PUSH	PSW
	MVI	A,-1
	CALL	COLOR
	LXI	H,JOYSS
	CALL	PRINT
	MVI	A,15
	CALL	SCR2
	POP	PSW
	POP	D
	CPI	19H
	MVI	E,-1
	JZ	JOYS30
	CPI	1AH
	MVI	E,1
	JZ	JOYS30
	CPI	20H
	JNZ	JOYS10
	MOV	A,D
	STA	SELJS
	CALL	JPOP
	JMP	BEG20
JOYS30:	MOV	A,D
	ADD	E
	ANA	A
	JP	$+5
	MVI	A,2
	CPI	3
	JC	$+4
	XRA	A
	ADI	2BH
	STA	JOYSS+2
	JMP	JOYS10
BEG40:	MOV	A,D
	ADD	E
	ANA	A
	CM	BEG42
	CPI	8
	CNC	BEG44
	ADI	2CH
	STA	MENUS+2
	JMP	BEG20
BEG42:	MVI	A,7
	RET
BEG44:	XRA	A
	RET
WINDOW:	SHLD	PRS01
	XCHG
	SHLD	PRS02
	XCHG
	PUSH	B
	MVI	A,9
	CALL	COLOR
	LXI	D,0FB04H
	DAD	D
	PUSH	H
	POP	B
	MVI	A,2
	CALL	PLOT
	LHLD	PRS02
	LXI	D,0FB04H
	DAD	D
	PUSH	H
	POP	B
	MVI	A,2
	CALL	LINE
	POP	B
	PUSH	B
	MOV	A,B
	CALL	COLOR
	LHLD	PRS01
	PUSH	H
	POP	B
	MVI	A,2
	CALL	PLOT
	LHLD	PRS02
	PUSH	H
	POP	B
	MVI	A,2
	CALL	LINE
	POP	B
	MOV	A,C
	CALL	COLOR
	MVI	D,2
WIND10:	PUSH	D
	LHLD	PRS01
	LXI	D,303H
	DAD	D
	SHLD	PRS01
	PUSH	H
	POP	B
	MVI	A,2
	CALL	PLOT
	LHLD	PRS02
	LXI	D,0FCFDH
	DAD	D
	SHLD	PRS02
	PUSH	H
	POP	B
	MVI	A,1
	CALL	LINE
	POP	D
	DCR	D
	JNZ	WIND10
	RET
CLS:	MVI	A,15
	CALL	SCR2
	MVI	C,13
	CALL	PRINTC
	MVI	C,1FH
	JMP	PRINTC
SCR3:	LXI	H,0C00H
	MVI	B,8
SCR30:	LDAX	D
	MOV	M,A
	INX	H
	INX	D
	DCR	B
	JNZ	SCR30
	RET
WAITK:	CALL	INKEY
	INR	A
	JNZ	WAITK
WAITK0:	CALL	INKEY
	INR	A
	JZ	WAITK0
	RET
STRED:	DB 'Управляющие клавиши в ре-'
	DB 'дакторе: <ПРОБЕЛ>-запись '
	DB 'уровня в память,<F3>- на '
	DB 'уровень вперед,<АР2>- на '
	DB 'уровень назад,<F4>-выход '
	DB 'из редактора,и курсорные '
	DB 'клавиши.',0
STRHL:	DB 'Уважаемые пользователи ПК'
	DB '"Вектор 06Ц" ! Представля'
	DB 'ю вам дисковую версию иг-'
	DB 'ры "Boulder Dash".       '
	DB 'В этой игре необходимо,уп'
	DB 'равляя человечком,собрать'
	DB 'все изображ╠нные на экра-'
	DB 'не алмазы, избегая при эт'
	DB 'ом встречи с бабочками и '
	DB 'минами.Но человечек может'
	DB 'не только убегать от них,'
	DB 'но и сбрасывать на них ка'
	DB 'мни.Камни можно двигать в'
	DB 'любом количестве и в лю- '
	DB 'ом направлении.После сбо-'
	DB 'ра всех алмазов на одном '
	DB 'уровне,человечек перехо- '
	DB 'дит на другой,за что ему '
	DB 'прибавляется одна жизнь. '
	DB 'Игра управляется клавиша-'
	DB 'ми со стрелками или любым'
	DB 'джойстиком.              '
	DB 'Программа имеет встроен- '
	DB 'ный редактор уровней,кото'
	DB 'рым можно изменить или по'
	DB 'лностью переделать любой '
	DB 'уровень.Новые уровни мож-'
	DB 'но сохранить на диске.   '
	DB 'Внимание ! Предлагаю всем'
	DB 'владельцам "Вектор 06Ц,им'
	DB 'еющим квазидиск и контрол'
	DB 'лер дисковода,стать заре-'
	DB 'гестрированным пользовате'
	DB 'лем ОС РДС,и пользоваться'
	DB 'льготами и скидками при  '
	DB 'покупке новых пакетов про'
	DB 'грамм,работающих в РДС и '
	DB 'других.Для получения инфо'
	DB 'рмации о новых программах'
	DB 'достаточно прислать один '
	DB 'или несколько конвертов,и'
	DB 'тогда по мере накопления '
	DB 'новых программ,будет рас-'
	DB 'сылаться информация о них'
	DB 'Уважаемые коллеги-програм'
	DB 'мисты ! Предлагаю вам наи'
	DB 'более  выгодные условия  '
	DB 'для продажи ваших програм'
	DB 'мных продуктов,60% от при'
	DB 'были за программы работаю'
	DB 'щие из РДС,и 40% за дру- '
	DB 'гие программы.Вы должны  '
	DB 'гарантировать качество св'
	DB 'оих программ,а я лично га'
	DB 'рантирую соблюдение ваше-'
	DB 'го авторского права.Пиши-'
	DB 'те письма,присылайте прог'
	DB 'раммы,желаю вам успехов в'
	DB 'нашем непростом труде.   '
	DB 'Мой адрес:184291,п.Ревда,'
	DB 'Мурманская обл.,а/я 14.  '
	DB 'Вьюнову Виталию Анатолье-'
	DB 'вичу.                    '
	DB '            До свидания. '
	DB 0
