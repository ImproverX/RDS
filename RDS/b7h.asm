	ORG	0AE00H
ZAPL:	EQU	0D380H	;DISP !
ZAPL2:	EQU	0C000H
BDSLDS:	EQU	0AC21H
CURDSK:	EQU	0A342H
DRIVSP:	EQU	0C000H
BDPRNT:	EQU	0A190H
BDDAT:	EQU	0ADB1H
BDDAT2:	EQU	0ADB9H
BDDAT3:	EQU	0ADD9H
INITCN:	EQU	0CA00H
PRINT:	EQU	0CA18H
KEYS:	EQU	0CA12H
KEY:	EQU	0CA03H
PRINTC:	EQU	0CA09H
HEXOUT:	EQU	0CA15H
INBYT:	EQU	0CA06H
OUTBYT:	EQU	0CA0CH
LST:	EQU	0CA0FH
RDMA:	EQU	0D900H
VDMA:	EQU	0DE80H
VIRT:	EQU	0DE00H
BATPAR:	EQU	0DD00H
SWAP:	EQU	0DD80H
DDMA:	EQU	0FF00H
P10:	EQU	20H
CPM:	EQU	0A011H
CPM0:	EQU	0A006H
CBIOS:
	JMP	COLD
	JMP	WBOOT
	JMP	KEYS
	JMP	KEY
	JMP	PRINTC
	JMP	LST
	JMP	PUTCH
	JMP	READER
	JMP	HOME
	JMP	SELDSK
	JMP	SETTRC
	JMP	SETSEC
	JMP	SETDMA
	JMP	READ
	JMP	WRITE
	JMP	LISTST
	JMP	SECTRN
	DW	DISKI
	JMP	VCPM
	JMP	BDSEAR
	JMP	BDREST
	JMP	NBDOS
	DW	HDDAT
TYPEWR:	DB	0
FTRACK:	DB	0
WOPER:	DB	0
BDSEAR:	SHLD	DMA
	CALL	READ10
	RET
BDREST:	LDA	CURDSK
	MOV	C,A
	LXI	H,1
	CALL	BDRS10
	CALL	0AD56H
	JMP	0AC21H
BDRS10:	INR	C
	DCR	C
	RZ
	DAD	H
	ADC	A
	JMP	BDRS10+1
COLD:	LDA	3EH
	ANA	A
	JZ	0FB03H
	DI
	DCR	A
	JZ	COLD10
	INR	A
	ANI	7FH
	JZ	COLD10
	STA	3EH
	MVI	A,0C9H
	STA	38H
	MVI	A,1
	STA	3FH
	LHLD	DISKI
	LXI	D,ZAGA
	PUSH	H
	PUSH	D
	MVI	C,3
COLD02:	PUSH	B
	MVI	B,8
	CALL	COPY10
	LXI	B,8
	DAD	B
	XCHG
	DAD	B
	XCHG
	POP	B
	DCR	C
	JNZ	COLD02
	POP	H
	LXI	B,93
	DAD	B
	XCHG
	POP	H
	DAD	B
	LXI	B,327
	CALL	BCOPY
	LXI	H,DRIVEA
	LXI	B,16
	SHLD	DISKI
	DAD	B
	SHLD	DISKI+2
	DAD	B
	SHLD	DISKI+4
	CALL	BDSLDS
	LXI	H,40H
	SHLD	6
	SHLD	9
	MVI	A,0C3H
	STA	5
	STA	8
	LXI	D,M40
	MVI	B,17
	XCHG
	CALL	COPY10
	RET
BCOPY:	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	BCOPY
	RET
M40:	DI
	LDA	15
	OUT	10H
	JMP	CBIOS+35H
	LDA	3CH
	OUT	10H
	MOV	A,C
	EI
	RET
COLD10:	STA	3EH
	MVI	A,0C3H
	STA	M38
	XRA	A
	STA	3FH
	CALL	INITCN
	CALL	BDSLDS
	RET
VCPM:	LXI	H,0
	DAD	SP
	SHLD	VCPM10+1
	LXI	H,38H
	MOV	A,M
	STA	M38
	MVI	M,0C9H
	LXI	SP,0E000H
	CALL	CPM0
	MOV	C,A
	DI
	LDA	M38
	STA	38H
VCPM10:	LXI	SP,0
	JMP	49H
M38:	DB	0
WBOOT:	DI
	LXI	H,0FB00H
	SHLD	6
	XRA	A
	STA	PISAT
	MVI	A,-1
	STA	ODISK
	LDA	4
	PUSH	PSW
	CALL	COM
	POP	PSW
	STA	4
	MOV	C,A
	EI
	LHLD	PUSK
	RET
PUSK:	DW	0
COM:	LDA	FLBAT
	ANA	A
	JNZ	FRCOM
	LDA	FACOM
	ANA	A
	JZ	FRCOM
	LXI	H,ACOMBF
	CALL	COM00
	JZ	FRCOM
	LXI	H,100H
	SHLD	PUSK
	XCHG
	JMP	COM20
FRCOM:	LXI	H,FILE
	CALL	COM00
	JZ	COMER
	JMP	COM02
COM00:	LXI	D,5CH
	MVI	B,12
	CALL	COPY10
	XCHG
	LXI	B,1800H
	CALL	FILL
	MVI	E,-1
	MVI	C,32
	CALL	CPM
	STA	OLDUSR
	MVI	E,0
	MVI	C,32
	CALL	CPM
	MVI	E,2
	MVI	C,14
	CALL	CPM
	LXI	D,5CH
	MVI	C,15
	CALL	CPM
	INR	A
	RET
COM02: 	LXI	D,80H
	MVI	C,26
	CALL	CPM
	MVI	C,20
	LXI	D,5CH
	CALL	CPM
	LHLD	80H
	XCHG
	LHLD	82H
	SHLD	PUSK
COM20:	PUSH	D
	MVI	C,26
	CALL	CPM
	LXI	D,5CH
	MVI	C,20
	CALL	CPM
	POP	D
	LXI	H,128
	DAD	D
	XCHG
	ANA	A
	JZ	COM20
	LDA	OLDUSR
	MOV	E,A
	MVI	C,32
	JMP	CPM
COMER:	LXI	H,STRCE
	CALL	PRINT
	DI
	HLT
STRCE:	DB 10,'Отсутствует файл C:COMMAND.SYS !',7,0
FILE:	DB 0,'COMMAND SYS'
OLDUSR:	DB	0
ACOMBF:	DS	12
PUTCH:	MOV	A,C
	JMP	OUTBYT
READER:	MOV	A,C
	JMP	INBYT
HOME:	MVI	C,0
SETTRC:	LXI	H,TRACK
	MOV	M,C
	RET
SETSEC:	LXI	H,SECT
	MOV	M,C
	RET
SETDMA:	MOV	H,B
	MOV	L,C
	SHLD	DMA
	RET
LISTST:	IN	5
	ANI	1
	RET
SECTRN:	MOV	H,B
	INR	C
	MOV	L,C
	RET
SELDSK:	LXI	H,0
	MOV	A,C
	CPI	3
	RNC
	STA	DISK
	CALL	VECT
DISKI:	DW	DRIVEA
	DW	DRIVEB
	DW	DRIVEC
VECT:	ADD	A
	POP	H
	MOV	E,A
	MVI	D,0
	DAD	D
	MOV	E,M
	INX	H
	MOV	H,M
	MOV	L,E
	RET
READ:	MVI	D,2
	LDA	DISK
	SUI	2
	JZ	READ02
	MVI	A,-1
	RP
	LXI	H,0
	DAD	SP
	SHLD	READSP+1
	LXI	SP,DRIVSP
	CALL	DRIV
READSP:	LXI	SP,0
	RET
READ02:	CALL	KVDR
	LXI	H,VDMA
	SHLD	WRKDMA
READ04:	PUSH	PSW
	CALL	READ10
	POP	PSW
	RET
READ10:	LHLD	WRKDMA
	XCHG
	LHLD	DMA
READ11:	XCHG
	MOV	A,D
	CPI	09FH
	JC	READ12
	CPI	0E0H
	JC	READ20
READ12:	MVI	B,16
	DI
	PUSH	H
	LXI	H,2
	DAD	SP
	SHLD	READ14+1
	POP	H
	SPHL
	XCHG
READ13:	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	DCR	B
	JNZ	READ13
READ14:	LXI	SP,0
	EI
	RET
READ20:	PUSH	H
	LXI	H,DIRBUF
	CALL	HDCMP
	POP	H
	JZ	READ12
	PUSH	H
	PUSH	D
	CALL	SWAPNG
	POP	D
	DI
	LXI	H,2
	DAD	SP
	SHLD	READ22+1
	POP	H
	LXI	SP,100H
	MVI	C,64
	CALL	80H
READ22:	LXI	SP,0
	EI
	LXI	H,SWAP
	LXI	D,128
	MVI	B,128
	CALL	COPY10
	RET
SWAPNG:	LXI	H,128
	LXI	D,VIRT
	LXI	B,SWAP
SWAP10:	MOV	A,M
	STAX	B
	LDAX	D
	MOV	M,A
	INR	C
	INR	E
	INR	L
	MOV	A,M
	STAX	B
	LDAX	D
	MOV	M,A
	INR	C
	INR	E
	INR	L
	JNZ	SWAP10
	RET
HDCMP:	MOV	A,H
	SUB	D
	RNZ
	MOV	A,L
	SUB	E
	RET
WRITE:	MVI	D,1
	LDA	DISK
	SUI	2
	JZ	WRITKV
	MVI	A,-1
	RP
	LXI	H,0
	DAD	SP
	SHLD	WRITSP+1
	LXI	SP,DRIVSP
	CALL	DRIV
WRITSP:	LXI	SP,0
	RET
WRITKV:	LXI	H,VDMA
	SHLD	WRKDMA
	CALL	WRIT10
	MVI	D,1
	JMP	KVDR
WRIT10:	LHLD	WRKDMA
	XCHG
	LHLD	DMA
	MOV	A,H
	CPI	09FH
	JC	READ12
	CPI	0E0H
	JNC	READ12
	PUSH	D
	LXI	D,DIRBUF
	CALL	HDCMP
	POP	D
	JZ	READ12
	PUSH	H
	PUSH	D
	CALL	SWAPNG
	POP	D
	DI
	LXI	H,2
	DAD	SP
	SHLD	WRIT22+1
	POP	H
	LXI	SP,100H
	LXI	B,128
	DAD	B
	XCHG
	DAD	B
	MVI	C,64
	CALL	83H
WRIT22:	LXI	SP,0
	EI
	LXI	H,SWAP
	LXI	D,128
	MVI	B,128
	CALL	COPY10
	RET
BDWRIT:	PUSH	D
	DI
	CALL	SWAPNG
	LXI	H,0
	POP	D
	DAD	SP
	SHLD	BDWR10+1
	LXI	H,VRTBUF
	LXI	B,40
	DAD	B
	XCHG
	DAD	B
	XCHG
	LXI	SP,100H
	MVI	C,20
	CALL	83H
BDWR10:	LXI	SP,0
	LXI	H,SWAP
	LXI	D,128
	MVI	B,128
	EI
	JMP	COPY10
BDREAD:	DI
	PUSH	H
	CALL	SWAPNG
	POP	D
	LXI	H,0
	DAD	SP
	SHLD	BDRD10+1
	LXI	H,VRTBUF
	LXI	SP,100H
	MVI	C,20
	CALL	80H
BDRD10:	LXI	SP,0
	LXI	H,SWAP
	LXI	D,128
	MVI	B,128
	EI
	JMP	COPY10
COPY:	LXI	H,PARAM
	LXI	D,PARAM0
	MVI	B,7
COPY10:	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	COPY10
	RET
KVDR:	DCR	D
	MOV	A,D
	STA	OPER
	PUSH	PSW
	CALL	COPY
	CALL	KVRAS
	POP	PSW
	JZ	KVWR
KVRD:	LXI	H,0
	DI
	DAD	SP
	SHLD	KVDR10+1
	CALL	RDSIX
	LHLD	KADR
	MOV	A,L
	SUI	80H
	MOV	L,A
	JNC	$+4
	DCR	H
	SPHL
	LXI	H,VDMA
	MVI	C,64
	MVI	B,0
KVRD10:	LDA	PORT10
	JMP	10H
KVRD11:	MOV	A,B
	MOV	M,E
	ADD	E
	INX	H
	MOV	M,D
	INX	H
	ADD	D
	MOV	B,A
	DCR	C
	JNZ	KVRD10
	MVI	A,1CH
	LXI	H,KVR11D
	SHLD	18H
	LHLD	KCRC
	SPHL
	JMP	10H
KVR11D:	MOV	A,E
	CMP	B
	JNZ	KVRD12
	CMP	B
	JZ	KVDR10
	MOV	A,D
	CMP	B
	MOV	E,D
	JZ	KVRD14
	MVI	C,2
KVDR10:	LXI	SP,0
	MVI	A,P10
	OUT	10H
	EI
	LXI	H,SIXBUF
	LXI	D,10H
	MVI	B,10
	CALL	COPY10
	MOV	A,C
	STA	ERROR
	ANA	A
	JNZ	FSWR20
	RET
KVRD12:	MOV	A,D
	CMP	B
	MOV	D,E
	JZ	KVRD14
	MVI	C,3
	JMP	KVDR10
KVRD14:	LXI	H,KVDR10
	SHLD	18H
	MVI	A,0D5H
	STA	12H
	MVI	A,1CH
	JMP	10H
KVWR:	LXI	H,0
	DI
	DAD	SP
	SHLD	KVDR10+1
	CALL	WRSIX
	LHLD	KADR
	LXI	D,128
	SPHL
	MVI	C,64
	MVI	B,0
	LXI	H,VDMA
	DAD	D
KVWR10:	DCX	H
	MOV	D,M
	MOV	A,B
	ADD	D
	DCX	H
	MOV	E,M
	ADD	E
	MOV	B,A
	LDA	PORT10
	JMP	10H
KVWR11:	DCR	C
	JNZ	KVWR10
	LHLD	KCRC
	INX	H
	INX	H
	SPHL
	MOV	D,B
	MOV	E,B
	JMP	KVRD14
RDSIX:	LXI	H,RDRU7
RDSIX0: LXI	D,10H
	LXI	B,SIXBUF
	MVI	A,10
SIXC10:	PUSH	PSW
	LDAX	D
	STAX	B
	MOV	A,M
	STAX	D
	INX	H
	INX	D
	INX	B
	POP	PSW
	DCR	A
	JNZ	SIXC10
	RET
WRSIX:	LXI	H,WRRU7
	JMP	RDSIX0
RDRU7:	OUT	10H
	POP	D
	MVI	A,P10
	OUT	10H
	JMP	KVRD11
WRRU7:	OUT	10H
	PUSH	D
	MVI	A,P10
	OUT	10H
	JMP	KVWR11
SIXBUF:	DS	10
KVRAS:	LDA	TRC0
	MOV	B,A
	MVI	A,0FBH
	SUB	B
	CPI	0F8H
	JNC	$+5
	SUI	10H
	CPI	38H
	JNC	$+5
	SUI	10H
	RRC
	RRC
	RRC
	RRC
	MOV	H,A
	ANI	0CH
	ORI	10H
	MOV	B,A
	MOV	A,H
	RRC
	RRC
	ORI	3
	MOV	H,A
	MVI	L,-1
	INX	H
	LDA	SECT0
	LXI	D,-128
KVRS10:	DCR	A
	JZ	KVRS12
	DAD	D
	JMP	KVRS10
KVRS12:	SHLD	KADR
	MOV	A,B
	STA	PORT10
	LDA	TRC0
	MOV	L,A
	MVI	H,0
	DAD	H
	DAD	H
	DAD	H
	DAD	H
	LDA	SECT0
	DCR	A
	ADD	A
	ADD	L
	MOV	L,A
	LXI	D,0F000H
	DAD	D
	SHLD	KCRC
	RET
KADR:	DW	0
KCRC:	DW	0
PORT10:	DB	0
DRIV:	CALL	DRIV20
	LDA	ERROR
	ANA	A
	RET
DRIV20:	DCR	D
	MOV	A,D
	STA	OPER
	PUSH	PSW
	CALL	COPY
	LXI	H,SECT0
	DCR	M
	MOV	A,M
	ANA	A
	RAR
	ANA	A
	RAR
	ANA	A
	RAR
	STA	RSECT
	POP	PSW
	JZ	DRIV22
	CALL	DRIV50
	JMP	DRIV30
DRIV50:	LDA	OOPER
	ANA	A
	RNZ
	LDA	TYPEWR
	DCR	A
	RZ
	LDA	OSECT0
	ANI	7
	CPI	7
	RZ
	JMP	FISWR
DRIV22:	LDA	OOPER
	ANA	A
	JNZ	DRI23V
	LDA	DISK0
	LXI	H,ODISK
	CMP	M
	JNZ	DRIV23
	LDA	OTRACK
	LXI	H,TRC0
	CMP	M
	JNZ	DRIV23
	LDA	RSECT
	LXI	H,OSECT
	CMP	M
	JNZ	DRIV23
	JMP	DRI23V
DRIV23:	LDA	PISAT
	ANA	A
	JNZ	DRI23V
	MVI	C,1
DRI23V:	MOV	A,C
	PUSH	PSW
	DCR	A
	CZ	DRIV50
	POP	PSW
	STA	TYPEWR
	LXI	H,OOPER
	MOV	A,M
	ANA	A
	LDA	OPER0
	MOV	M,A
	JNZ	DRIV32
DRIV30:	LDA	PISAT
	ANA	A
	CNZ	FISWR
	LDA	OPER0
	STA	OOPER
	XRA	A
	STA	PISAT
	LXI	H,DISK0
	LDA	ODISK
	CMP	M
	JNZ	DRIV32
	LXI	H,TRC0
	LDA	OTRACK
	CMP	M
	JNZ	DRIV32
	LDA	RSECT
	LXI	H,OSECT
	CMP	M
	JNZ	DRIV32
	LDA	OPER0
	ANA	A
	JZ	DRIV34
	LDA	SECT0
	ANI	7
	JNZ	DRIV34
DRIV32:	LDA	DISK0
	STA	ODISK
	LDA	TRC0
	STA	OTRACK
	LDA	RSECT
	STA	OSECT
	CALL	FISRD
DRIV34:	LDA	SECT0
	ANI	7
	RAR
	MOV	H,A
	MVI	A,0
	RAR
	MOV	L,A
	LXI	D,RDMA
	DAD	D
	SHLD	WRKDMA
	LDA	OPER0
	DCR	A
	JZ	DRIV35
	CALL	WRIT10
	JMP	DRIV36
DRIV35:	CALL	READ10
DRIV36:	LDA	OPER0
	DCR	A
	RZ
	LDA	TYPEWR
	CPI	1
	JZ	DRIV40
	LDA	SECT0
	STA	OSECT0
	ANI	7
	CPI	7
	RNZ
	MVI	A,1
	STA	PISAT
	RET
DRIV40:	XRA	A
	STA	PISAT
	CALL	FISWR
	RET
TSTHDD:	LXI	H,HDDA
	LDA	ODISK
	ANA	A
	JZ	$+6
	LXI	H,HDDB
	MOV	A,M
	INX	H
	ORA	M
	RET
FISRD:	MVI	A,8
	STA	WOPER
	CALL	TSTHDD
	JNZ	RDHDD
FSRD00:	DI
	CALL	POWER
	LDA	OSECT
	INR	A
	OUT	19H
	LXI	H,RDMA
	MVI	C,3
	MVI	A,80H
	OUT	1BH
FSRD10:	IN	1BH
	RRC
	JNC	FSRD10
	DCX	H
	MOV	A,M
FSRD11:	MOV	M,A
	INX	H
FSRD12:	IN	1BH
	ANA	C
	JPO	FSRD12
	IN	18H
	JM	FSRDNR
	JNZ	FSRD11
	MOV	A,B
	OUT	1CH
	IN	1BH
	ANI	0DDH
	STA	ERROR
	EI
	RZ
	ANI	10H
	CNZ	TRAN99
	LXI	H,WOPER
	DCR	M
	JNZ	FSRD00
	LDA	ERROR
	ANI	10H
	JZ	FSWR20
	LXI	H,STRSEE
	CALL	ASK
	JMP	FISRD
FSRDNR:	LXI	H,STRNTR
	CALL	ASK
	JMP	FSRD00
FISWR:	MVI	A,8
	STA	WOPER
	CALL	TSTHDD
	JNZ	WRHDD
FSWR00:	DI
	CALL	POWER
	LDA	OSECT
	INR	A
	OUT	19H
	LXI	H,RDMA
	MVI	C,83H
	MVI	A,0A0H
	OUT	1BH
FSWR10:	IN	1BH
	RRC
	JNC	FSWR10
FSWR11:	IN	1BH
	ANA	C
	JPO	FSWR11
	MOV	A,M
	OUT	18H
	INX	H
	JM	FSWRNR
	JNZ	FSWR11
	MOV	A,B
	OUT	1CH
	IN	1BH
	ANI	0DDH
	STA	ERROR
	MOV	H,A
	EI
	ANI	40H
	JNZ	WRPROT
	MOV	A,H
	ANA	A
	RZ
	ANI	10H
	CNZ	TRAN99
	LXI	H,WOPER
	DCR	M
	JNZ	FSWR00
	LDA	ERROR
	ANI	10H
	JZ	FSWR20
	LXI	H,STRSEE
	CALL	ASK
	JMP	FISWR
FSWR20:	LDA	3FH
	CPI	2
	RNC
	DCR	A
	RZ
	LXI	H,STR1
	CALL	FSWR22
	LDA	ERROR
	CALL	HEXOUT
	CALL	FSWR22
	LDA	OPER0
	CALL	HEXOUT
	CALL	FSWR22
	LDA	DISK0
	CALL	HEXOUT
	CALL	FSWR22
	LDA	TRC0
	CALL	HEXOUT
	CALL	FSWR22
	LDA	SECT0
	CALL	HEXOUT
	CALL	FSWR22
	CALL	KEYE
	ANI	5FH
	CPI	'D'
	JZ	FSSC10
	CPI	'Y'
	JZ	FSSC10
	LDA	ERROR
	RET
FSWRNR:	LXI	H,STRNTR
	CALL	ASK
	JMP	FSWR00
FSWR22:	MOV	A,M
	INX	H
	ANA	A
	RZ
	MOV	C,A
	CALL	PRINTC
	JMP	FSWR22
KEYE:	CALL	KEY
	PUSH	PSW
	MOV	C,A
	CALL	PRINTC
	POP	PSW
	RET
STR1:	DB 10,'ERR=',0,' OPR=',0,' DSK=',0,' TRK=',0,' SEC=',0
STR0:	DB 10,' ОШИБКА ДИСКА,ПРОДОЛЖИМ ?',7,0
STRSEE:	DB 10,' ОШИБКА ПОЗИЦИОНИРОВАНИЯ !',7,0
STRSE0:	DB 10,'1-Повторить,2-Прервать ?',0
STRWPR:	DB 10,' ДИСК ЗАЩИЩ╟Н ОТ ЗАПИСИ !',7,0
STRNTR:	DB 10,' ДИСК НЕ ГОТОВ !',7,0
FSSC10:	XRA	A
	STA	ERROR
	RET
PRINTH:	MOV	A,H
	CALL	0CA15H
	MOV	A,L
	JMP	0CA15H
ASK:	CALL	PRINT
	LXI	H,STRSE0
	CALL	PRINT
	LDA	3FH
	DCR	A
	RZ
	EI
	CALL	KEYE
	DI
	CPI	31H
	RZ
	JMP	0
WRPROT:	LXI	H,STRWPR
	CALL	ASK
	JMP	FSWR00
PORT1C:	DB	34H
POWER:	CALL	MOTOR
	LDA	OTRACK
	ANA	A
	RAR
	STA	FTRACK
	LDA	ODISK
	ANI	1
	LXI	H,TTRC
	JZ	$+4
	INX	H
	MOV	A,M
	OUT	1AH
	LDA	FTRACK
	MOV	M,A
TRAN2F:	OUT	18H
TRAN30:	MVI	A,10H
	OUT	1BH
	CALL	WAIT
TRAN31:	MOV	A,B
	OUT	1CH
	IN	1BH
	RRC
	JC	TRAN31
	LDA	PORT1C
	OUT	1CH
WAIT:	MVI	A,60H
	DCR	A
	JNZ	$-1
	RET
TRAN99:	XRA	A
	OUT	1BH
	CALL	WAIT
TRAN9A:	MOV	A,B
	OUT	1CH
	IN	1BH
	RRC
	JC	TRAN9A
	CALL	WAIT
	LDA	FTRACK
	JMP	TRAN2F
MOTOR:	LDA	ODISK
	ANI	3
	MOV	H,A
	LDA	OTRACK
	ANI	1
	XRI	1
	RLC
	RLC
	ORA	H
	MOV	B,A
	ORI	30H
	STA	PORT1C
	LXI	H,0
MOTR10:	MOV	A,B
	OUT	1CH
	IN	1BH
	RLC
	RNC
	XTHL
	XTHL
	DCX	H
	XTHL
	XTHL
	MOV	A,H
	ORA	L
	JNZ	MOTR10
	MVI	A,80H
	STA	ERROR
	LXI	H,STRNTR
	CALL	ASK
	JMP	MOTR10-3
NBDSPR:	MOV	C,E
	JMP	BDPRNT
NBDOS:	MOV	A,C
	CPI	2
	JZ	NBDSPR
	LXI	H,TBTRAP
NBDS00:	MOV	A,M
	ANA	A
	JZ	NBDS10
	CMP	C
	INX	H
	JNZ	NBDS00
	MOV	A,D
	CPI	9FH
	JC	CPM
	CPI	0E0H
	JNC	CPM
	PUSH	D
	PUSH	B
	CALL	BDWRIT
	LXI	D,VRTBUF
	POP	B
	CALL	CPM
	XTHL
	PUSH	D
	PUSH	PSW
	CALL	BDREAD
	POP	PSW
	POP	D
	POP	H
	RET
TBTRAP:	DB 15,16,17,19,20,21,22,23,30,33,34,35,36,40,0
NBDS10:	MOV	A,C
	CPI	80H
	JNZ	CPM
	MOV	A,B
	CPI	16
	JNC	CPM
	MOV	C,B
	MVI	B,0
	LXI	H,NBDSTB
	DAD	B
	DAD	B
	MOV	A,M
	INX	H
	MOV	H,M
	MOV	L,A
	PCHL
NBDSTB:	DW TEST,USED,CINP,COUT,COUCLS,IBAT,BATS
	DW GBATP,GFBAT,VBIOS,FBAT,SCOM,DCOM
	DW CHKDSK,GETHDD,SETHDD
CHKDSK:	MVI	C,29
	CALL	CPM
	XCHG
	LDA	CURDSK
	LXI	H,1
	MOV	C,A
	CALL	BDRS10
	MOV	A,D
	ANA	H
	MOV	H,A
	MOV	A,E
	ANA	L
	ORA	H
	RZ
	JMP	BDREST
SCOM:	LXI	H,ACOMBF
	XCHG
	MVI	B,12
	CALL	COPY10
	MVI	A,-1
	STA	FACOM
	RET
DCOM:	INR	E
	LDA	FACOM
	RZ
	XRA	A
	STA	FACOM
	RET
VBIOS:	XCHG
	MOV	E,M
	INX	H
	MOV	C,M
	INX	H
	MOV	B,M
	LXI	H,CBIOS+6
	MOV	A,E
	ADD	A
	ADD	E
	CALL	HLA
	PCHL
GFBAT:	LDA	FLBAT
	RET
GBATP:	XCHG
	MOV	C,M
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	LXI	H,BATPAR
	MOV	A,M
	ANA	A
	JZ	GBAT10
	INX	H
	INR	C
GBAT04:	CALL	GBAT02
	JC	GBAT10
	INX	H
	JZ	GBAT04
	DCX	H
	DCR	C
	JZ	GBAT06
GBAT05:	CALL	GBAT02
	JC	GBAT10
	INX	H
	JNZ	GBAT05
	JMP	GBAT04
GBAT06:	MOV	A,M
	STAX	D
	INX	H
	INX	D
	CALL	GBAT02
	JC	GBAT07
	JNZ	GBAT06
GBAT07:	XRA	A
	STAX	D
	XCHG
	RET
GBAT10:	MVI	A,-1
	RET
GBAT02:	MOV	A,M
	CPI	20H
	RZ
	CPI	9
	RZ
	ANA	A
	STC
	RZ
	CMC
	RET
CINP:	LDAX	D
	ANA	A
	JNZ	CINP02
	PUSH	D
	MVI	C,25
	CALL	CPM
	INR	A
	POP	D
	STAX	D
CINP02:	LXI	H,CINPBF
	PUSH	H
	XCHG
	MVI	B,12
	CALL	COPY10
	XCHG
	LXI	B,1800H
	CALL	FILL
	POP	D
	MVI	C,15
	CALL	CPM
	CPI	-1
	RZ
	XRA	A
	STA	ACCTYP
	LXI	H,CINDAT
	CALL	ACCE20
	JNZ	CINP10
	XRA	A
	STA	CINSCN
	DCR	A
	STA	FLCINP
	STA	KLAV
	LXI	H,CINKEY
	SHLD	CBIOS+10
CINP10:	XRA	A
	RET
IBAT:	LDAX	D
	ANA	A
	JNZ	IBAT02
	PUSH	D
	MVI	C,25
	CALL	CPM
	INR	A
	POP	D
	STAX	D
IBAT02:	LXI	H,BATBF
	PUSH	H
	XCHG
	MVI	B,12
	CALL	COPY10
	XCHG
	LXI	B,1800H
	CALL	FILL
	LXI	H,80H
	LXI	D,BATPAR
	MVI	B,128
	CALL	COPY10
	POP	D
	MVI	C,15
	CALL	CPM
	CPI	-1
	RZ
	XRA	A
	STA	ACCTYP
	LXI	H,BATDAT
	CALL	ACCE20
	JNZ	IBAT10
IBAT04:	XRA	A
	STA	BATSCN
	DCR	A
	STA	FLBAT
	STA	BTKLAV
IBAT10:	XRA	A
	RET
FBAT:	LXI	H,80H
	LXI	D,BATDMA
	MVI	B,128
	CALL	COPY10
	JMP	IBAT04
COUT:	LDAX	D
	ANA	A
	JNZ	COUT02
	PUSH	D
	MVI	C,25
	CALL	CPM
	POP	D
	INR	A
	STAX	D
COUT02:	LXI	H,COUTBF
	PUSH	H
	XCHG
	MVI	B,12
	CALL	COPY10
	XCHG
	LXI	B,1800H
	CALL	FILL
	POP	D
	MVI	C,15
	CALL	CPM
	CPI	-1
	MVI	A,-1
	RNZ
	LXI	D,COUTBF
	MVI	C,22
	CALL	CPM
	CPI	-1
	RZ
	LXI	H,COUDMA
	LXI	B,801AH
	CALL	FILL
	XRA	A
	STA	COUSCN
	DCR	A
	STA	FLCOUT
	LXI	H,COUPRN
	SHLD	CBIOS+13
	XRA	A
	RET
COUCLS:	LDA	FLCOUT
	ANA	A
	RZ
	MVI	A,-1
	STA	ACCTYP
	LXI	H,COUDAT
	CALL	ACCE20
	LXI	D,COUTBF
	MVI	C,16
	CALL	CPM
	XRA	A
	STA	FLCOUT
	LXI	H,PRINTC
	SHLD	CBIOS+13
	RET
COUPRN:	LXI	H,COUDAT
	CALL	WACCES
	CNZ	COUCLS
	JMP	PRINTC
CINKEY:	LDA	FLCINP
	INR	A
	JNZ	CINK10
	LXI	H,CINDAT
	CALL	CINK00
	RC
CINK10:	XRA	A
	STA	FLCINP
	LXI	H,KEY
	SHLD	CBIOS+10
	JMP	KEY
BATS:	LDA	FLBAT
	ANA	A
	MVI	A,1AH
	RZ
	LXI	H,BATDAT
	CALL	CINK00
	RC
	XRA	A
	STA	FLBAT
	MVI	A,1AH
	RET
CINK00:	DCX	H
	MOV	A,M
	CPI	-1
	JNZ	CINK08
	INX	H
	CALL	RACCES
	JNZ	CINK09
	PUSH	PSW
	CALL	TS0D0A
	JNZ	CINK06
	CALL	RACCES
	DCX	H
	MVI	M,1AH
	JNZ	CINK06
	CALL	TS0D0A
	MVI	M,-1
	JZ	CINK06
	MOV	M,A
CINK06:	POP	PSW
CINK08:	CPI	1AH
	STC
	RNZ
CINK09:	XRA	A
	RET
TS0D0A:	CPI	10
	RZ
	CPI	13
	RET
WACCES:	MVI	A,-1
	JMP	ACCES
RACCES:	XRA	A
ACCES:	STA	ACCTYP
	PUSH	H
	MOV	A,M
	ANA	A
	JP	ACCE10
	PUSH	B
	CALL	ACCE20
	POP	B
	POP	H
	RNZ
	PUSH	H
	XRA	A
	MOV	M,A
ACCE10:	INR	M
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	CALL	HLA
	LDA	ACCTYP
	INR	A
	JZ	ACCE12
	XRA	A
	MOV	A,M
	POP	H
	RET
ACCE12:	MOV	M,C
	POP	H
	RET
ACCE20:	XCHG
	LHLD	BDDAT
	PUSH	H
	LHLD	BDDAT2
	PUSH	H
	LHLD	BDDAT3
	PUSH	H
	XCHG
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	C,M
	INX	H
	MOV	B,M
	LDA	ACCTYP
	INR	A
	JZ	ACCE22
	PUSH	D
	PUSH	B
	LXI	D,DIRBUF
	MVI	C,1AH
	CALL	CPM
	POP	D
	MVI	C,20
	CALL	CPM
	POP	D
	LXI	H,DIRBUF
	PUSH	PSW
	MVI	B,128
	CALL	COPY10
	POP	PSW
	ANA	A
ACCE21:	POP	H
	SHLD	BDDAT3
	POP	H
	SHLD	BDDAT2
	POP	H
	SHLD	BDDAT
	RET
ACCE22:	PUSH	D
	PUSH	B
	LXI	H,DIRBUF
	PUSH	H
	XCHG
	MVI	B,128
	CALL	COPY10
	POP	D
	MVI	C,1AH
	CALL	CPM
	POP	D
	MVI	C,21
	CALL	CPM
	POP	H
	PUSH	PSW
	LXI	B,801AH
	CALL	FILL
	POP	PSW
	ANA	A
	JMP	ACCE21
HLA:	ADD	L
	MOV	L,A
	RNC
	INR	H
	RET
TEST:	LDA	3FH
	PUSH	PSW
	MVI	A,2
	STA	3FH
	LXI	H,DIRBUF
	SHLD	DMA
	XRA	A
	STA	TRACK
	INR	A
	STA	SECT
	MVI	C,2
	CALL	SELDSK
TEST20:	CALL	READ
	ANA	A
	JZ	TEST22
	LXI	H,STRTS
	CALL	FSWR22
	LDA	ERROR
	CALL	HEXOUT
	CALL	FSWR22
	LDA	TRACK
	CALL	HEXOUT
	CALL	FSWR22
	LDA	SECT
	CALL	HEXOUT
TEST22:	LXI	H,SECT
	INR	M
	MOV	A,M
	CPI	9
	JC	TEST20
	MVI	M,1
	LXI	H,TRACK
	INR	M
	MOV	A,M
	CPI	220
	JC	TEST20
	POP	PSW
	STA	3FH
	XRA	A
	RET
STRTS:	DB 10,'ОШИБКА КВАЗИДИСКА -',0
	DB ',ДОРОЖКА -',0,',СЕКТОР -',0
ZAGA:	DW 0,0,0,0,DIRBUF,PARA,CSVA,ALVA
ZAGB:	DW 0,0,0,0,DIRBUF,PARB,CSVB,ALVB
ZAGC:	DW 0,0,0,0,DIRBUF,PARC,0,ALVC
PARA:	DW 40
	DB 4,15,0
	DW 187H,127,0C0H,32,8
PARB:	DW 40
	DB 4,15,0
	DW 187H,127,0C0H,32,8
PARC:	DW 8
	DB 3,7,0
	DW 219,63,0C0H,0,0
DIRBUF:	DS	128
CSVA:	DS	32
CSVB:	DS	32
ALVA:	DS	51
ALVB:	DS	51
ALVC:	DS	33
TTRC:	DB 0,0
PARAM0:	DS	7
PARAM:	DS	7
ODISK:	DB	-1
OTRACK:	DB	-1
OOPER:	DB	1
OSECT:	DB	-1
RSECT:	DB	-1
PISAT:	DB	0
DISK:	EQU	PARAM
ERROR:	EQU	3DH
OPER:	EQU	PARAM+2
TRACK:	EQU	PARAM+3
SECT:	EQU	PARAM+4
DMA:	EQU	PARAM+5
DISK0:	EQU	PARAM0
OPER0:	EQU	PARAM0+2
TRC0:	EQU	PARAM0+3
SECT0:	EQU	PARAM0+4
DRIVEA:	EQU	ZAGA
DRIVEB:	EQU	ZAGB
DRIVEC:	EQU	ZAGC
DMA0:	EQU	PARAM0+5
VRTBUF:	DS	40
OSECT0:	DB	0
WRKDMA:	DW	0
USED:	MVI	C,31
	CALL	CPM
	INX	H
	INX	H
	MOV	A,M
	CPI	3
	PUSH	PSW
	INX	H
	INX	H
	INX	H
	MOV	A,M
	INX	H
	MOV	H,M
	MOV	L,A
	INX	H
	PUSH	H
	PUSH	H
	MVI	C,27
	CALL	CPM
	POP	D
	LXI	B,0
USED10:	PUSH	H
	MOV	L,M
	MVI	H,8
USED12:	MOV	A,L
	RLC
	MOV	L,A
	JC	$+4
	INX	B
	DCX	D
	MOV	A,D
	ORA	E
	JZ	USED14
	DCR	H
	JNZ	USED12
	POP	H
	INX	H
	JMP	USED10
USED14:	POP	H
	POP	D
	MOV	H,B
	MOV	L,C
	DCX	D
	DCX	D
	POP	PSW
	RZ
	DAD	H
	XCHG
	DAD	H
	XCHG
	RET
FILL:	MOV	M,C
	INX	H
	DCR	B
	JNZ	FILL
	RET
KLAV:	DB	0
CINDAT:
CINSCN:	DB	0
	DW	CINDMA
	DW	CINPBF
FLCINP:	DB	0
COUDAT:
COUSCN:	DB	0
	DW	COUDMA
	DW	COUTBF
FLCOUT:	DB	0
BTKLAV:	DB	0
BATDAT:
BATSCN:	DB	0
	DW	BATDMA
	DW	BATBF
FLBAT:	DB	0
ACCTYP:	DB	0
FACOM:	DB	0
CINPBF:	DS	36
CINDMA:	EQU	ZAPL
COUTBF:	DS	36
COUDMA:	EQU	ZAPL2
BATBF:	DS	36
BATDMA:	DS	128
HDDA:	DB	1,0,2,0,0
HDDB:	DB	0,0,0,0,0
HDDAT:	DW	APAR0
	DW	APAR1
	DW	APAR2
SETHDD:	LDA	4
	CPI	2
	MOV	C,A
	MVI	A,0FFH
	RNC
APAR2:	LXI	H,-1
	DAD	D
	RC
	LXI	H,HDDB
	DCR	C
	JZ	$+6
	LXI	H,HDDA
	MOV	M,E
	INX	H
	MOV	M,D
	PUSH	H
	LXI	H,0F3BEH
	MVI	A,-1
	INX	D
SETH10:	LXI	B,1570
	DAD	B
	ACI	0
	DCX	D
	MOV	B,A
	MOV	A,D
	ORA	E
	MOV	A,B
	JNZ	SETH10
	XCHG
	POP	H
	INX	H
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	MOV	M,A
	XRA	A
	RET
GETHDD:	LHLD	APAR2+1
	CALL	NEGHL
	DCX	H
	PUSH	H
	LXI	H,HDDB
	DCR	E
	JZ	$+6
	LXI	H,HDDA
	MOV	E,M
	INX	H
	MOV	D,M
	POP	H
	XCHG
	RET
NEGHL:	MOV	A,L
	CMA
	MOV	L,A
	MOV	A,H
	CMA
	MOV	H,A
	INX	H
	RET
WRHDD:	MVI	C,1
	JMP	HDD
RDHDD:	MVI	C,0
HDD:	PUSH	H
	LHLD	OTRACK
	MVI	H,000H
	MOV	D,H
	MOV	E,L
	DAD	H
	DAD	H
	DAD	D
	LDA	OSECT
	MOV	E,A
	MVI	D,0
	DAD	D
	DAD	H
	LDA	OTRACK
	CPI	8
	JNC	AD85F
	POP	D
	LXI	D,2
	XRA	A
	JMP	AD86C
AD853:	POP	D
	CPI	0FFH
	LXI	D,0F60AH	;ЧТО БЫ 0 СЕКТ.
	JZ	AD86C
	JMP	HDDERR
AD85F:	CPI	0A5H
	JNC	AD853
	XTHL
	INX	H
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	A,M
	POP	H
AD86C:	DAD	D
	ACI	000H
	MOV	E,L
	MOV	L,H
	MOV	H,A
	CALL	READY
	JZ	HDDERR
	PUSH	D
	CALL	AD8F3	;ВЫЧ. СТ.БТ.ЦИЛ
	OUT	055H
	POP	D
	MOV	H,L
	MOV	L,E
	CALL	AD8F3	;МЛ.БТ.ЦИЛ.
	OUT	054H
APAR0:	LXI	D,0FF00H
	CALL	AD904
	LXI	D,00010H
	CALL	AD8FC
	OUT	056H
	MOV	A,L
	INR	A
	OUT	053H
REPOP:	MVI	A,2
	OUT	052H
	LXI	H,RDMA
	MVI	A,020H
	INR	C
	DCR	C
	JZ	AD8A9
	MVI	A,030H
AD8A9:	OUT	057H
	MVI	E,2
AD8AD:	CALL	READY
	JZ	ERHDD
	IN	057H
	ANI	008H
	JZ	ERHDD
	MVI	B,2
AD8BC:	INR	C
	DCR	C
	JNZ	AD8CF
AD8C1:	IN	050H
	MOV	M,A
	INR	L
	IN	058H
	MOV	M,A
	INR	L
	JNZ	AD8C1
	JMP	AD8DC
AD8CF:	INR	L
	MOV	A,M
	OUT	058H
	DCR	L
	MOV	A,M
	OUT	050H
	INR	L
	INR	L
	JNZ	AD8CF
AD8DC:	INR	H
	DCR	B
	JNZ	AD8BC
	DCR	E
	JNZ	AD8AD
	CALL	READY
	JZ	ERHDD
	ANI	0DDH
	CPI	050H
	RZ
ERHDD:	LXI	H,WOPER
	DCR	M
	JNZ	REPOP
	JMP	HDDERR
APAR1:
AD8F3:	LXI	D,0FC00H
	CALL	AD904
	LXI	D,00040H
AD8FC:	MVI	B,0FFH
AD8FE:	ADD	B
	DAD	D
	JNC	AD8FE
	RET
AD904:	XRA	A
	MVI	B,010H
AD907:	ADD	B
	DAD	D
	JC	AD907
	RET
READY:	PUSH	D
	PUSH	B
	MVI	D,005H
AD9DD:	IN	057H
	ANI	0C0H
	CPI	040H
	JZ	AD9F2
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	AD9DD
	DCR	D
	JNZ	AD9DD
	OUT	05FH
AD9F2:	ANA	A
	POP	B
	POP	D
	IN	057H
	RET
HDDERR:	IN	057H
	RRC
	IN	051H
	JC	ADA02
	XRA	A
ADA02:	MOV	B,A
	IN	057H
	ANI	020H
	ORA	B
	MOV	B,A
	IN	057H
	ANI	0C0H
	CPI	040H
	MOV	A,B
	JZ	ADA15
	ORI	010H
ADA15:	OUT	05FH
	STA	ERROR
	JMP	FSWR20	;На обработку ошибки