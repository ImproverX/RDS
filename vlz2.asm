	ORG	100H
	JMP	START
BYTDIR:	EQU	0C800H
BYTFAT:	EQU	0CA00H
WELEM:	EQU	2051	;Размер обрабатываемого буфера.
FRSSMC:	EQU	66	;Короткое смещение+1.
SCNSMC:	EQU	515
ZAPAS:	EQU	2048+128
ZAPAS2:	EQU	17
UNPAK:	EQU	800H
MBUF:	EQU	900H
OUTBUF:	EQU	4900H
OPENF:	MVI	C,15
	JMP	5
CLOSEF:	MVI	C,16
	JMP	5
ERAF:	MVI	C,19
	JMP	5
SREAD:	MVI	C,20
	JMP	5
SWRITE:	MVI	C,21
	JMP	5
MAKEF:	MVI	C,22
	JMP	5
RENF:	MVI	C,23
	JMP	5
SETDMA:	MVI	C,26
	JMP	5
STR0:	DB 10,7,'Отсутствует входной файл !$'
STR1:	DB 10,7,'Нет места в каталоге диска !$'
STR2:	DB 10,7,'Немогу записать выходной файл,диск полон !$'
STR3:	DB 10,'Упаковка завершена !$'
STR4:	DB 10,7,'Отсутствует выходной файл!$'
KADR:	DW	0
START:	LXI	SP,0
	LDA	5DH
	CPI	20H
	LXI	D,STR0
	JZ	EXITMS
	LDA	6DH
	CPI	20H
	LXI	D,STR4
	JZ	EXITMS
	LXI	H,6CH
	LXI	D,OUTFIL
	MVI	B,12
	CALL	COPY
	CALL	CLEAR
	LXI	D,5CH
	CALL	OPENF
	INR	A
	LXI	D,STR0
	JZ	EXITMS
	MVI	B,128
	CALL	RDINBF
	LXI	H,OUTFIL+12
	CALL	CLEAR+3
	LXI	D,OUTFIL
	CALL	ERAF
	LXI	D,OUTFIL
	CALL	MAKEF
	LXI	D,STR1
	INR	A
	JZ	EXITMS
	LXI	H,EXTCOM
	LXI	D,OUTFIL+9
	MVI	B,3
	CALL	STRCMP
	CZ	COMSAV
	LXI	H,0
	SHLD	FLPNTI
	SHLD	FLPNTO
	CALL	INITB
	DI
PAK:	IN	1
	ADD	A
	JP	PAKEOF
	CALL	GTNXCH
	JC	PAKEOF
	LHLD	FLPNTI
	SHLD	FLPNI2
	LXI	D,MBUF
	DAD	D
	DCX	H
	SHLD	TEKADR
	CALL	EXIST
	JNZ	PAK10
	XRA	A
	STA	BSTLEN
PAK20:	PUSH	H
	LHLD	FLPNI2
	SHLD	FLPNTI
	POP	H
	LXI	D,BYTFAT
	PUSH	H
	DAD	H
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	XTHL
	XCHG
	LHLD	FATPNT
	CALL	HDCMP
	JZ	$+6
	JNC	GTSM10
	MOV	A,E
	SUB	L
	CMA
	MOV	E,A
	MOV	A,D
	SBB	H
	CMA
	MOV	D,A
	INX	D
	LHLD	FATLEN
	DAD	D
	XCHG
	MOV	A,E
	CMA
	MOV	E,A
	MOV	A,D
	CMA
	MOV	D,A
	INX	D
	JMP	PAK21
GTSM10:	MOV	A,L
	SUB	E
	CMA
	MOV	E,A
	MOV	A,H
	SBB	D
	CMA
	MOV	D,A
	INX	D
PAK21:	LHLD	TEKADR
	PUSH	H
	DAD	D
	SHLD	CSMECH
	POP	D
	MVI	B,1
	INX	H
PAK22:	CALL	HDCMP
	JZ	PAK24
	SHLD	SRVADR
	CALL	GTNXCH
	JC	PAK23
	LHLD	SRVADR
	CMP	M
	JNZ	PAK24
	INX	H
	INR	B
	MOV	A,B
	CPI	35
	JC	PAK22
PAK23:	MOV	A,B
	STA	BSTLEN
	POP	H
	LHLD	CSMECH
	SHLD	BSTSMC
	JMP	PAK30
PAK24:	LXI	H,BSTLEN
	MOV	A,B
	CMP	M
	JC	PAK26
	CPI	3
	JNZ	PAK25
	PUSH	H
	LHLD	CSMECH
	XCHG
	LHLD	TEKADR
	MOV	A,L
	SUB	E
	MOV	L,A
	MOV	A,H
	SBB	D
	MOV	H,A
	LXI	D,SCNSMC
	CALL	HDCMP
	POP	H
	MOV	A,B
	JNC	PAK26
PAK25:	MOV	M,A
	LHLD	CSMECH
	SHLD	BSTSMC
PAK26:	POP	H
	MOV	A,H
	ANI	80H
	JZ	PAK20
PAK30:	LDA	BSTLEN
	CPI	2
	JC	PAK40
	JNZ	PAK31
	LHLD	BSTSMC
	XCHG
	LHLD	TEKADR
	MOV	A,L
	SUB	E
	MOV	L,A
	MOV	A,H
	SBB	D
	MOV	H,A
	LXI	D,FRSSMC
	CALL	HDCMP
	JNC	PAK40
PAK31:	CALL	WRCODE
	LHLD	TEKADR
	MOV	A,M
	CALL	POVTOR
	JC	PAK_31
	LXI	H,BSTLEN
	LDA	PVTLEN
	CMP	M
	JC	PAK_31
	JNZ	POVTS
	LXI	H,SKCODE
	LDA	SKCOD2
	CMP	M
	JC	POVTS
PAK_31:	CALL	WRCD22
	LXI	H,CODE1
	LDA	SKCODE
	MOV	B,A
PAK_32:	MOV	A,M
	CALL	WRBYTE
	INX	H
	DCR	B
	JNZ	PAK_32
	LDA	BSTLEN
	MOV	B,A
	LHLD	TEKADR
PAK32:	PUSH	B
PAK33:	PUSH	H
	PUSH	B
	SHLD	TEKADR
	MOV	A,M
	CALL	INS
	POP	B
	POP	H
	INX	H
	DCR	B
	JNZ	PAK33
	POP	B
	LHLD	FLPNI2
	MOV	C,B
	MVI	B,0
	DCR	C
	DAD	B
	SHLD	FLPNTI
	JMP	PAK
PAK40:	LHLD	FLPNI2
	DCX	H
	SHLD	FLPNTI
	CALL	GTNXCH
PAK10:	LDA	CURBYT
	CALL	POVTOR
	JNC	POVTS
	LDA	CURBYT
	CALL	INS
	LDA	CURBYT
	CALL	WRCD20
	JMP	PAK
POVTOR:	LHLD	FLPNI2
	SHLD	FLPNTI
	MOV	C,A
	MVI	B,1
POVT10:	CALL	GTNXCH
	JC	POVT12
	CMP	C
	JNZ	POVT12
	INR	B
	MOV	A,B
	CPI	17
	JC	POVT10
	MOV	A,C
	ANA	A
	JNZ	POVT12
	MOV	A,B
	CPI	32
	JC	POVT10
POVT12:	MOV	A,B
	STA	PVTLEN
	MOV	A,C
	ANA	A
	JZ	POVT31
	MOV	A,B
	CPI	3
	JNC	POVT21
POVT14:	MOV	A,C
	STA	CURBYT
	LHLD	FLPNI2
	SHLD	FLPNTI
	STC
	RET
POVTS:	CALL	WRCD22
	LXI	H,CODE3
	LDA	SKCOD2
	MOV	B,A
POVTS0:	MOV	A,M
	CALL	WRBYTE
	INX	H
	DCR	B
	JNZ	POVTS0
	LHLD	TEKADR
	LDA	PVTLEN
	MOV	B,A
	JMP	PAK32
POVT21:	MOV	A,B
	SUI	3
	ADI	0F0H
	STA	CODE3
	MOV	A,C
	STA	CODE4
	MVI	A,2
	STA	SKCOD2
	ANA	A
	RET
POVT31:	MOV	A,B
	CPI	2
	JC	POVT14
	MOV	A,B
	SUI	2
	ADI	0D0H
	STA	CODE3
	MVI	A,1
	STA	SKCOD2
	ANA	A
	RET
INS:	MOV	C,A
	CALL	EXIST
	JZ	INS10
INS02:	PUSH	D
	CALL	SRLAST
	PUSH	H
	PUSH	D
	CALL	INCBUF
	POP	D
	POP	H
	XCHG
	POP	H
	MOV	M,E
	INX	H
	MOV	M,D
	XCHG
	DAD	H
	LXI	D,BYTFAT
	DAD	D
	MVI	M,-1
	INX	H
	MVI	M,-1
	RET
INS10:	LXI	D,BYTFAT
	DAD	H
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	MOV	A,H
	ANI	80H
	JZ	INS10
	DCX	D
	JMP	INS02
SRLAST:	LHLD	FATLEN
	XCHG
	LHLD	FATPNT
	DAD	D
	LXI	D,WELEM
	CALL	HDCMP
	JC	SRLS10
	LXI	D,-WELEM
	DAD	D
SRLS10:	RET
INCBUF:	LHLD	FATLEN
	LXI	D,WELEM
	CALL	HDCMP
	JNZ	INCB12
	LHLD	FATPNT
	CALL	DELEL
	INX	H
	CALL	HDCMP
	JNZ	INCB11
	LXI	D,-WELEM
	DAD	D
INCB11:	SHLD	FATPNT
	JMP	INCB14
INCB12:	INX	H
	SHLD	FATLEN
INCB14:	RET
DELEL:	PUSH	H
	PUSH	D
	DAD	H
	LXI	D,BYTFAT
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	PUSH	D
	LHLD	TEKADR
	LXI	D,-WELEM
	DAD	D
	MOV	A,M
	CALL	EXIST
	XCHG
	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	POP	D
	POP	H
	RET
SEKDIR:	MOV	L,A
	MVI	H,0
	DAD	H
	LXI	D,BYTDIR
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	DCX	H
	XCHG
	RET
EXIST:	CALL	SEKDIR
	MOV	A,H
	ANI	80H
	RET
WRCODE:	LDA	BSTLEN
	MOV	B,A
	LHLD	BSTSMC
	XCHG
	LHLD	TEKADR
	MOV	A,L
	SUB	E
	MOV	L,A
	MOV	A,H
	SBB	D
	MOV	H,A
	LXI	D,SCNSMC
	CALL	HDCMP
	JNC	WRCD30
	LXI	D,FRSSMC
	CALL	HDCMP
	JNC	WRCD10
	MOV	A,B
	CPI	4
	JNC	WRCD10
	SUI	2
	DCR	L
	DCR	L
	RRC
	RRC
	ORA	L
	ADI	30H
	STA	CODE1
	MVI	A,1
	STA	SKCODE
	RET
WRCD10:	MOV	A,B
	CPI	19
	JNC	WRCD30
	DCX	H
	DCX	H
	DCX	H
	MOV	A,B
	SUI	3
	MOV	E,A
	MOV	A,H
	RRC
	RRC
	RRC
	RRC
	ORA	E
	ADI	0B0H
	STA	CODE1
	MOV	A,L
	STA	CODE2
	MVI	A,2
	STA	SKCODE
	RET
WRCD30:	DCX	H
	DCX	H
	DCX	H
	DCX	H
	MVI	A,0EFH
	STA	CODE1
	MOV	A,B
	SUI	4
	RLC
	RLC
	RLC
	ORA	H
	STA	CODE2
	MOV	A,L
	STA	CODE2X
	MVI	A,3
	STA	SKCODE
	RET
WRCD20:	LXI	H,LENNPK
	MOV	C,A
	INR	M
	MOV	A,M
	DCR	A
	LXI	H,NPKBUF
	CALL	HLA
	MOV	M,C
	LDA	LENNPK
	CPI	48
	RC
WRCD22:	LDA	LENNPK
	ANA	A
	RZ
WRCD23:	MOV	B,A
	DCR	A
	CALL	WRBYTE
	LXI	H,NPKBUF
WRCD24:	MOV	A,M
	CALL	WRBYTE
	INX	H
	DCR	B
	JNZ	WRCD24
	XRA	A
	STA	LENNPK
	RET
WRBYTE:	PUSH	H
	PUSH	D
	PUSH	B
	MOV	C,A
	LHLD	FLPNTO
	LXI	D,OUTBUF
	DAD	D
	MOV	M,C
	INX	H
	LXI	D,OUTBUF+4000H
	CALL	HDCMP
	CZ	WRBT10
	LHLD	FLPNTO
	INX	H
	SHLD	FLPNTO
	POP	B
	POP	D
	POP	H
	RET
WRBT10:	LXI	H,-1
	SHLD	FLPNTO
	JMP	WROUBF
PAKEOF:	LXI	SP,0
	CALL	WRCD22
	MVI	A,-1
	CALL	WRBYTE
	LHLD	FLPNTO
PAKE10:	MOV	A,L
	ANI	7FH
	JZ	PAKE12
	MVI	A,1AH
	CALL	WRBYTE
	LHLD	FLPNTO
	JMP	PAKE10
PAKE12:	DCX	H
	MVI	B,7
PAKE13:	MOV	A,H
	ANA	A
	RAR
	MOV	H,A
	MOV	A,L
	RAR
	MOV	L,A
	DCR	B
	JNZ	PAKE13
	MOV	B,L
	INR	B
	CALL	WROUBF+2
	LXI	D,OUTFIL
	CALL	CLOSEF
	LXI	D,STR3
	JMP	EXITMS
INITB:	LXI	H,BYTDIR
	LXI	B,1800H
	MVI	E,-1
	CALL	BFILL
	LXI	H,0
	SHLD	FATLEN
	SHLD	FATPNT
	RET
BFILL:	MOV	M,E
	INX	H
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	BFILL
	RET
GTNXCH:	PUSH	H
	PUSH	D
	PUSH	B
	LHLD	FLPNTI
	LXI	D,MBUF
	DAD	D
	LDA	EOFINF
	ANA	A
	JZ	GTNX10
	XCHG
	LHLD	KADR
	XCHG
	CALL	HDCMP
	STC
	JZ	GTNX20
GTNX10:	LXI	D,MBUF+4000H
	CALL	HDCMP
	CZ	GTNX12
	MOV	A,M
	LHLD	FLPNTI
	INX	H
	SHLD	FLPNTI
	STA	CURBYT
	ANA	A
GTNX20:	POP	B
	POP	D
	POP	H
	RET
GTNX12:	LXI	H,MBUF+4000H-ZAPAS
	LXI	D,MBUF
	LXI	B,ZAPAS
	CALL	BCOPY
	PUSH	D
	LXI	H,ZAPAS
	SHLD	FLPNTI
	LHLD	FLPNI2
	LXI	D,-4000H+ZAPAS
	DAD	D
	SHLD	FLPNI2
	LHLD	TEKADR
	DAD	D
	SHLD	TEKADR
	LHLD	CSMECH
	DAD	D
	SHLD	CSMECH
	LHLD	SRVADR
	DAD	D
	SHLD	SRVADR
	POP	D
	PUSH	D
	MVI	B,128-ZAPAS2
	CALL	RDIB10
	POP	H
	RET
STRCMP:	LDAX	D
	CMP	M
	RNZ
	INX	H
	INX	D
	DCR	B
	JNZ	STRCMP
	RET
COPY:	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	COPY
	RET
BCOPY:	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCX	B
	MOV	A,B
	ORA	C
	JNZ	BCOPY
	RET
COMSAV:	LXI	D,UNPAK
	PUSH	D
	CALL	SETDMA
	LXI	D,OUTFIL
	CALL	SWRITE
	LXI	H,128
	POP	D
	DAD	D
	XCHG
	CALL	SETDMA
	LXI	D,OUTFIL
	CALL	SWRITE
	RET
HLA:	ADD	L
	MOV	L,A
	RNC
	INR	H
	RET
HDCMP:	MOV	A,H
	SUB	D
	RNZ
	MOV	A,L
	SUB	E
	RET
EXITMS:	MVI	C,9
	LXI	SP,0
	CALL	5
	JMP	0
RDINBF:	LXI	D,MBUF
RDIB10:	PUSH	B
	PUSH	D
	CALL	SETDMA
	LXI	D,5CH
	CALL	SREAD
	ANA	A
	JNZ	RDIB12
	POP	D
	LXI	H,128
	DAD	D
	XCHG
	POP	B
	DCR	B
	JNZ	RDIB10
	XRA	A
	STA	EOFINF
	RET
RDIB12:	POP	H
	SHLD	KADR
	POP	B
	MVI	A,-1
	STA	EOFINF
	RET
WROUBF:	MVI	B,128
	LXI	D,OUTBUF
WROB10:	PUSH	B
	PUSH	D
	CALL	SETDMA
	LXI	D,OUTFIL
	CALL	SWRITE
	ANA	A
	JNZ	WROB12
	POP	D
	LXI	H,128
	DAD	D
	XCHG
	POP	B
	DCR	B
	JNZ	WROB10
	RET
WROB12:	LXI	SP,0
	LXI	D,STR2
	JMP	EXITMS
CLEAR:	LXI	H,68H
	LXI	B,1800H
FILL:	MOV	M,C
	INX	H
	DCR	B
	JNZ	FILL
	RET
CURBYT:	DB	0
FLPNTI:	DW	0
FLPNTO:	DW	0
LENNPK:	DB	0
FATPNT:	DW	0
FATLEN:	DW	0
CSMECH:	DW	0
BSTLEN:	DB	0
BSTSMC:	DW	0
FLPNI2:	DW	0
PVTLEN:	DB	0
SKCODE:	DB	0
SKCOD2:	DB	0
CODE1:	DB	0
CODE2:	DB	0
CODE2X:	DB	0
CODE3:	DB	0
CODE4:	DB	0
EOFINF:	DB	0
TEKADR:	DW	0
SRVADR:	DW	0
OUTFIL:	DS	36
EXTCOM:	DB	'COM'
NPKBUF:	DS	48
